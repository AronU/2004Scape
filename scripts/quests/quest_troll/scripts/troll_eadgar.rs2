[apnpc1,troll_eadgar]
if(inzone(0_44_157_0_0, 0_44_157_63_63, npc_coord) = true) {
    if (npc_range(coord) > 4) { // osrs
        p_aprange(4);
        return;
    }
    if(%troll_freed_eadgar = ^true) {
        ~chatnpc("<p,happy>Don't worry about me, I'll be out in a jiffy. Talk to me at my home.");
        return;
    }
    ~chatplayer("<p,happy>How are you doing in there?");
    ~chatnpc("<p,scared>I'm fine! Just dandy! A bit cold, I keep asking them to install a fireplace but they don't listen.");
    ~chatplayer("<p,neutral>I haven't found the key.");
    ~chatnpc("<p,scared>It's on the guard's belt, silly. Free me and I can cook you something good!");
    return;
}
if (npc_range(coord) > 1) { 
    p_aprange(1);
    return;
}
~chatplayer("<p,happy>Hi!");
switch_int(random(4)) {
    case 0 : ~chatnpc("<p,happy>Welcome to Mad Eadgar's! Happiness in a bowl!");
    case 1 : ~chatnpc("<p,happy>Welcome to Mad Eadgar's! You'll never want to leave!");
    case 2 : ~chatnpc("<p,happy>Welcome to Mad Eadgar's! We don't serve trolls!");
    case 3 : ~chatnpc("<p,happy>Welcome to Mad Eadgar's! Serving all day, every day!");
}
~chatnpc("<p,happy>Would you care to sample our delicious home cooking?");
switch_int(~p_choice3("Why do you live so close to the trolls?", 1, "What do you have to offer?", 2, "No thanks, Eadgar.", 3)) {
    case 1 :
        ~chatplayer("<p,confused>Why do you live so close to the trolls? Isn't it dangerous?");
        ~chatnpc("<p,happy>Well, I suppose I do keep getting captured by the trolls and thrown in prison...But they always release me in the end, I'm far too old and skinny for their tastes.");
        ~chatnpc("<p,happy>In any case, this is my home, and I'm not leaving it. And this area has the tastiest goats!");
    case 2 :
        ~chatplayer("<p,confused>What do you have to offer?");
        ~chatnpc("<p,happy>The chef's recommendation for today is mountain goat stew. I'll give some stew in exchange for logs for my fire. They're hard to come by around here.");
        switch_int(~p_choice2("I'd like some mountain goat stew, please.", 1, "No thanks, Eadgar.", 2)) {
            case 1 :
                ~chatplayer("<p,neutral>I'd like some mountain goat stew, please.");
                if(inv_totalcat(inv, category_22) = 0) {
                    ~chatnpc("<p,angry>You don't have any logs! I may be mad, but I'm not stupid!");
                    return;
                }
                ~inv_delcat(inv, category_22);
                ~chatnpc("<p,happy>Here you go! Enjoy!");
                if_close;
                mes("You get a bowl of mountain goat stew.");
                anim(human_eat, 0);
                sound_synth(eat, 0, 0);
                p_delay(1);
                mes("You eat the mountain goat stew.");
                if (stat(hitpoints) < stat_base(hitpoints)) {
                    mes("It heals some health.");
                    stat_heal(hitpoints, 20, 0);
                }
            case 2 : @eadgar_nothanks;
        }
    case 3 : @eadgar_nothanks;
}

[label,eadgar_nothanks]
~chatplayer("<p,neutral>No thanks, Eadgar.");
~chatnpc("<p,happy>Your loss!");