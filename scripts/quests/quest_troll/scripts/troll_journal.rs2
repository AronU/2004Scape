[if_button,questlist:troll]
def_string $text;
if(%troll_quest = ^troll_not_started) {
    $text = "@dbl@I can start this quest by speaking to @dre@Denulth@dbl@ in his tent at|@dbl@the @dre@Imperial Guard Camp@dbl@ in @dre@Burthrope@dbl@ after completing the|@dre@Death Plateau Quest";
    $text = append($text, "||@dbl@To complete this quest I need:|@dre@Level 15 Agility|@dbl@I also need to be able to defeat a @dre@level 113 Troll|@dre@Level 30 Thieving@dbl@ might be useful.");
    ~quest_journal("Troll Stronghold", $text);
    return;
}

if(%troll_quest >= ^troll_started & %troll_quest < ^troll_freed_godric) {
    $text = "@dbl@I promised @dre@Denulth@dbl@ that I would rescue @dre@Godric@dbl@ from the @dre@Troll|@dre@Stronghold";
}
if(%troll_told = ^true | inv_total(inv, death_climbingboots) > 0 | inv_total(worn, death_climbingboots) > 0) {
    $text = append($text, "||@str@I got some climbing boots from Tenzing.");
}

if(%troll_quest >= ^troll_defeated_dad) {
    $text = append($text, "||@str@I have defeated the Troll Champion.");
} else if(%troll_told = ^true) {
    $text = append($text, "||@dbl@I have to defeat the @dre@Troll Champion@dbl@ to get past the @dre@Arena");
} else if(%troll_accepted_challenge = ^true) {
    $text = append($text, "||@dbl@I have accepted the @dre@Troll Champion@dbl@'s challenge.");
}

if(%troll_quest >= ^troll_defeated_dad) {
    if(%troll_entered_stronghold = ^false) {
        $text = append($text, "||@dbl@I have to find a way to get into the @dre@Troll Stronghold");
    } else {
        $text = append($text, "||@str@I have found my way into the Troll Stronghold");
    }

    if(%troll_quest >= ^troll_entered_prison) {
        $text = append($text, "||@str@I have found my way into the prison.");
    } else if(inv_total(inv, troll_key_prison) > 0) {
        $text = append($text, "||@str@I have the prison key.");
        $text = append($text, "|@dbl@I have to get into the @dre@prison");
    } else {
        $text = append($text, "||@dbl@I have to find a way to get into the @dre@prison");
    }
}

if(%troll_quest >= ^troll_freed_godric) {
    $text = append($text, "||@str@I've rescued Godric");
    if(%troll_freed_eadgar = ^true) $text = append($text, " and Mad Eadgar");
    $text = append($text, ".");
    if(%troll_quest < ^troll_complete) $text = append($text, "|@dbl@I should return and tell @dre@Dunstan@dbl@ his son is safe.");
} else if(%troll_quest = ^troll_entered_prison) {
    $text = append($text, "||@dbl@I have to rescue @dre@Godric");
}

if(%troll_quest = ^troll_complete) {
    $text = append($text, "||@str@I talked to Dunstan and he gave me the Law Talisman");
    $text = append($text, "|@str@as a token of thanks!");
    $text = append($text, "||@red@QUEST COMPLETE!");
}
~quest_journal("Troll Stronghold", $text);