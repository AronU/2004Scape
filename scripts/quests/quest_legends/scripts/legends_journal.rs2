[if_button,questlist:legends]
def_string $text;

if(%legendsquest = ^legends_complete) {
    $text = "@str@Radimus Erkle asked me to go to map the Kharazi Jungle. @str@In addition, he asked me to meet with the tribe of the @str@Kharazi Jungle and return with some sort of gift or token @str@to display in the Legends Guild.";
    $text = append($text, "|@str@Once I'd mapped the jungle I met up with a Kharazi tribe @str@member called Gujuo, he told me that his tribe was @str@scattered throughout the jungle. He asked me to make a @str@new Yommi tree totem pole in order to gather the peoples @str@of the Kharazi tribe together.");
    $text = append($text, "|@str@In the process of getting a Yommi tree, I released a @str@Shaman from being possessed by a Demon called @str@Nezikchened. I managed to slay the demon eventually but @str@he kept reappearing!");
    $text = append($text, "|@str@I eventually managed to slay the demon and replace the @str@evil totem pole with a good one. Gujuo managed to call all @str@of his people together and they rewarded me for my @str@efforts with a gilded totem pole which is now on display in @str@the Legends Guild for all to see.");
    $text = append($text, "|@str@Radimus Erkle granted me access to the Legends' Guild|@str@facilities.");
    $text = append($text, "|@red@QUEST COMPLETE!");
    ~quest_journal("The Legends Quest", $text);
    return;
}

if(%legendsquest = ^legends_not_started) {
    $text = "@dbl@I can start this quest by speaking to @dre@Radimus Erkle@dbl@ in the";
    $text = append($text, "|@dre@Legends Guild@dbl@, North East of @dre@Ardougne@dbl@.");
    $text = append($text, "|@dbl@To start this quest I need to complete the following:-");
    def_int $quest_reqs_completed = ^true;
    if(%heroquest >= ^hero_complete) {
        $text = append($text, "|@str@'Heroes Quest'");
    } else {
        $text = append($text, "|@dre@'Heroes Quest'");
        $quest_reqs_completed = ^false;
    }

    if(%crestquest >= ^crest_complete) {
        $text = append($text, "|@str@'Family Crest Quest'");
    } else {
        $text = append($text, "|@dre@'Family Crest Quest'");
        $quest_reqs_completed = ^false;
    }

    if(%zombiequeen >= ^zombiequeen_complete) {
        $text = append($text, "|@str@'Shilo Village Quest'");
    } else {
        $text = append($text, "|@dre@'Shilo Village Quest'");
        $quest_reqs_completed = ^false;
    }

    if(%upass >= ^upass_complete) {
        $text = append($text, "|@str@'Underground Pass Quest'");
    } else {
        $text = append($text, "|@dre@'Underground Pass Quest'");
        $quest_reqs_completed = ^false;
    }

    if(%waterfall_quest >= ^waterfall_complete) {
        $text = append($text, "|@str@'Waterfall Quest'");
    } else {
        $text = append($text, "|@dre@'Waterfall Quest'");
        $quest_reqs_completed = ^false;
    }

    if(%qp >= ^legends_required_questpoints) {
         $text = append($text, "|@str@And gain a total of 107 quest points.");
    } else {
        $text = append($text, "|@dbl@And gain a total of @dre@107 quest points@dbl@.");
        $quest_reqs_completed = ^false;
    }
    if($quest_reqs_completed = ^true) $text = append($text, "|@dbl@I have all required @dre@quests@dbl@ and @dre@quest points@dbl@.");

    $text = append($text, "|@dbl@To complete this quest I need:-");
    def_int $skill_reqs_completed = ^true;
    if(stat_base(magic) >= 56) {
         $text = append($text, "|@str@Level 56 magic");
    } else {
        $text = append($text, "|@dre@Level 56 magic");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(mining) >= 52) {
         $text = append($text, "|@str@Level 52 mining");
    } else {
        $text = append($text, "|@dre@Level 52 mining");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(agility) >= 50) {
         $text = append($text, "|@str@Level 50 agility");
    } else {
        $text = append($text, "|@dre@Level 50 agility");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(crafting) >= 50) {
         $text = append($text, "|@str@Level 50 crafting");
    } else {
        $text = append($text, "|@dre@Level 50 crafting");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(smithing) >= 50) {
         $text = append($text, "|@str@Level 50 smithing");
    } else {
        $text = append($text, "|@dre@Level 50 smithing");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(strength) >= 50) {
         $text = append($text, "|@str@Level 50 strength");
    } else {
        $text = append($text, "|@dre@Level 50 strength");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(thieving) >= 50) {
         $text = append($text, "|@str@Level 50 thieving");
    } else {
        $text = append($text, "|@dre@Level 50 thieving");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(woodcutting) >= 50) {
         $text = append($text, "|@str@Level 50 woodcutting");
    } else {
        $text = append($text, "|@dre@Level 50 woodcutting");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(herblore) >= 45) {
         $text = append($text, "|@str@Level 45 herblore");
    } else {
        $text = append($text, "|@dre@Level 45 herblore");
        $skill_reqs_completed = ^false;
    }

    if(stat_base(prayer) >= 42) {
         $text = append($text, "|@str@Level 42 prayer");
    } else {
        $text = append($text, "|@dre@Level 42 prayer");
        $skill_reqs_completed = ^false;
    }
    if($skill_reqs_completed = ^true) $text = append($text, "|@dbl@I have all the necessary skills to complete this quest");
    ~quest_journal("The Legends Quest", $text);
    return;
}
// https://github.com/Indio3/quest-journals/blob/master/quest_legends/whattodoforlegends.png, no requirements striked out, even though 
// the mapping part doesnt normally show until you have other items in inv, safe assumption is it just showed everything at once originally
if(%legendsquest >= ^legends_swung_bullroarer) {
    $text = "@str@Radimus Erkle asked me to map the Kharazi jungle";
    $text = append($text, "|@str@and bring back a gift to display in the Legends Guild.");
} else {
    $text = "@dre@Radimus Erkle@dbl@ asked me to map the @dre@Kharazi jungle@dbl@";
    $text = append($text, "|@dbl@and bring back a @dre@gift@dbl@ to display in the @dre@Legends Guild.");
}
if(%legendsquest >= ^legends_mapped_jungle) {
    $text = append($text, "|@str@I have mapped the Kharazi Jungle for Radimus Erkle.");
} else {
    $text = append($text, "|@dbl@To get in to the @dre@Kharazi Jungle@dbl@ I'll need-:");
    $text = append($text, "|@dbl@An @dre@axe.");
    $text = append($text, "|@dbl@A @dre@machette.");
    $text = append($text, "|@dbl@The @dre@Radimus notes.");
    $text = append($text, "|@dbl@To map the @dre@Kharazi Jungle@dbl@ I'll need:-.");
    $text = append($text, "|@dre@Papyrus");
    $text = append($text, "|@dre@Charcoal");
}

if(%legendsquest >= ^legends_swung_bullroarer) {
    $text = append($text, "|@str@A local forester took a copy of my Kharazi map and in");
    $text = append($text, "|@str@return I got a bullroarer which I used to attract 'Gujuo'.");
} else if(%legendsquest = ^legends_got_bullroarer) {
    $text = append($text, "|@str@A local forester took a copy of my Kharazi map and in");
    $text = append($text, "|@str@return I got a bullroarer which may attract a native.");
} else if(%legendsquest = ^legends_mapped_jungle) {
    $text = append($text, "|@dbl@I need to make friends with a @dre@native@dbl@ and bring back");
    $text = append($text, "|@dbl@a @dre@gift@dbl@ which can be displayed in the @dre@Legends Guild.");
}
if(%legendsquest > ^legends_accepted_rescue_ungadulu) {
    $text = append($text, "|@str@I agreed to help Gujuo by freeing a Shaman. The shaman @str@is in some caves in North West Kharazi.");
} else if(%legendsquest = ^legends_accepted_rescue_ungadulu) {
    $text = append($text, "|@dbl@I agreed to help @dre@Gujuo@dbl@ by releasing a @dre@Shaman@dbl@ who is @dbl@being held in some @dre@caves@dbl@ in @dre@North West Kharazi@dbl@.");
} else if(%legendsquest = ^legends_swung_bullroarer) {
    $text = append($text, "|@dbl@I've met '@dre@Gujuo@dbl@', a @dre@Kharazi Jungle native@dbl@. Perhaps I can @dbl@make friends with him?");
} else if(%legendsquest = ^legends_got_bullroarer) {
    $text = append($text, "|@dbl@I need to make friends with a @dre@native@dbl@ and bring back");
    $text = append($text, "|@dbl@a @dre@gift@dbl@ which can be displayed in the @dre@Legends Guild.");
}

if(%legendsquest >= ^legends_spoke_ungadulu) {
    $text = append($text, "|@str@The Shaman Ungadulu is trapped in a flaming octagon.");
} else if(%legendsquest = ^legends_found_entrance) {
    $text = append($text, "|@dbl@I need to get some @dre@Yommi tree seeds@dbl@ from @dre@Ungadulu@dbl@.");
}

if(%legendsquest > ^legends_spoke_ungadulu) {
    $text = append($text, "|@str@Ungadulu acted strange and mentioned sacred water.");
} else if(%legendsquest = ^legends_spoke_ungadulu) {
    $text = append($text, "|@dre@Ungadulu@dbl@ is acting weird and talking a lot of nonsense, @dbl@perhaps someone else can help me understand what he's @dbl@talking about.");
    $text = append($text, "|@dbl@I need to get some @dre@Yommi tree seeds@dbl@ from @dre@Ungadulu@dbl@.");
}

if(%legendsquest >= ^legends_summoned_nezikchened_fire) {
    $text = append($text, "|@str@I used sacred water on flames to get into octagon.");
} else if(%legendsquest >= ^legends_filled_bowl) {
    $text = append($text, "|@str@Gujuo said I need a blessed vessel of sun metal.");
} else if(%legendsquest = ^legends_asked_gujuo_holy_water) {
    if(inv_total(inv, goldbowlbless_empty) > 0 | inv_total(inv, goldbowlbless_water) > 0) {
        $text = append($text, "|@str@Gujuo said I need a blessed vessel of sun metal.");
        $text = append($text, "|@dbl@I have a @dre@blessed golden bowl@dbl@, now I need some @dre@sacred @dre@water@dbl@.");
    } else if(inv_total(inv, goldbowl_empty) > 0 | inv_total(inv, goldbowl_water) > 0 | inv_total(inv, goldbowl_pure) > 0) {
        $text = append($text, "|@str@Gujuo said I need a blessed vessel of sun metal.");
        $text = append($text, "|@dbl@I have a @dre@golden bowl@dbl@, now it needs to be @dre@blessed@dbl@.");
    } else {
        $text = append($text, "|@dbl@Gujuo mentioned a @dre@blessed vessel@dbl@ made from @dre@metal@dbl@ of the @dre@sun@dbl@. Perhaps I can get some @dre@sacred water@dbl@ with this?");
    }
}
if(%legendsquest >= ^legends_summoned_nezikchened_fire) {
    $text = append($text, "|@str@I used the book of binding on Ungadulu and|@str@a demon called Nezikchened appeared. I think that @str@Ungadulu is possessed by this demon.");
} else if(%legendsquest = ^legends_filled_bowl) {
    $text = append($text, "|@dbl@I have some @dre@sacred water@dbl@. @dre@Ungadulu@dbl@ mentioned that it @dbl@may help me to get closer to him. I need get some @dre@Yommi @dre@tree seeds@dbl@ from @dre@Ungadulu@dbl@.");
    if(inv_total(inv, book_of_binding) > 0) {
        $text = append($text, "|@dbl@I found a '@dre@book of binding@dbl@' which may give some clue as to @dbl@how I can release @dre@Ungadulu@dbl@.");
    }
}

if(%legendsquest >= ^legends_defeated_nezikchened_fire) {
    $text = append($text, "|@str@I used the book of binding to force the demon @str@Nezikchened out of Ungadulu. I then slew the demon.");
} else if(%legendsquest >= ^legends_summoned_nezikchened_fire) {
    $text = append($text, "|@dbl@I need to release @dre@Ungadulu@dbl@ so that I can get some @dre@Yommi @dre@tree seeds@dbl@ from him.");
}

if(%legendsquest >= ^legends_water_pool_dried_up) {
    $text = append($text, "|@str@Ungadulu studies supernatural spirits!");
    $text = append($text, "|@str@I used sacred water to germinate the Yommi tree seeds @str@that Ungadulu gave me.");

} else if(%legendsquest = ^legends_germinated_seeds) {
    $text = append($text, "|@dbl@I have some @dre@germinated Yommi tree seeds@dbl@. I just need to @dbl@plant them now in some @dre@fertile soil@dbl@.");
    $text = append($text, "|@dre@Ungadulu@dbl@ studies supernatural @dre@spirits!");
} else if(%legendsquest = ^legends_defeated_nezikchened_fire) {
    if(inv_total(inv, yommiseeds) > 0) {
        $text = append($text, "|@dbl@I have some @dre@Yommi tree seeds@dbl@ from @dre@Ungadulu@dbl@. I need to @dre@germinate@dbl@ them with @dre@sacred water@dbl@ before planting them in @dre@fertile soil@dbl@.");
        $text = append($text, "|@dre@Ungadulu@dbl@ studies supernatural @dre@spirits!");
    } else {
        $text = append($text, "|@dbl@I need to get some @dre@Yommi tree seeds@dbl@ from @dre@Ungadulu@dbl@ now @dbl@that I have released him from his possession.");
    }
}

if(%legendsquest >= ^legends_enter_lower_dungeon) {
    $text = append($text, "|@str@Gujuo said the sacred water source is in the Viyeldi caves. @str@I had to make a bravery potion in order to enter the @str@caves. The Viyeldi caves are under the caves where @str@Ungadulu is.");
} else if(%legendsquest = ^legends_talk_gujuo_pool) {
    if(inv_total(inv, bravery_pot) > 0) {
        $text = append($text, "|@str@Gujuo said the sacred water source is in the Viyeldi caves.");
        $text = append($text, "|@str@I have made a bravery potion in order to enter the caves.");
        $text = append($text, "|@dbl@I need to find the @dre@Viyeldi @dbl@caves and the source of the @dre@sacred water@dbl@.");
    } else {
        if(inv_total(inv, snake_weed) > 0 & inv_total(inv, ardrigal) > 0) {
            $text = append($text, "|@dbl@I need to make the @dre@bravery potion@dbl@ using @dbl@the @dre@Snake weed @dbl@and @dre@Ardrigal @dbl@I have.");
        } else {
            $text = append($text, "|@dre@Gujuo@dbl@ said the @dre@Viyeldi caves@dbl@ might be the @dre@source@dbl@ of the @dre@sacred water@dbl@. I need to make a @dre@bravery potion@dbl@ before I @dbl@can enter.");
            $text = append($text, "|@dbl@I need to collect:-");
            if(inv_total(inv, snake_weed) > 0) $text = append($text, "|@str@I have snake weed.");
            else $text = append($text, "|@dre@Snake weed");
            if(inv_total(inv, ardrigal) > 0) $text = append($text, "|@str@I have Ardrigal.");
            else $text = append($text, "|@dre@Ardrigal.");
        }
    }
} else if(%legendsquest = ^legends_water_pool_dried_up) {
    $text = append($text, "|@dbl@I need to get some more @dre@sacred water@dbl@ in order for the @dre@Yommi tree@dbl@ to grow.");
}

if(%legendsquest = ^legends_crystal_pieces_smelted) {
    $text = append($text, "|@str@I fixed chunks of crystal into dragon heart shape.");
} else if(%legendsquest = ^legends_enter_lower_dungeon) {
    $text = append($text, "|@dbl@I'm able to access the @dre@Viyeldi Caves@dbl@. I need to find the @dbl@source of @dre@sacred water@dbl@, which may be protected.");
    def_int $crystal_count;
    if(inv_total(inv, heartcrystal_sectiona) > 0) {
        $crystal_count = add($crystal_count, 1);
    } 
    if(inv_total(inv, heartcrystal_sectionb) > 0) {
        $crystal_count = add($crystal_count, 1);
    } 
    if(inv_total(inv, heartcrystal_sectionc) > 0) {
        $crystal_count = add($crystal_count, 1);
    }
    def_int $crystal_furnace_count;
    if(testbit(%legends_bits, ^legends_smelting_chunk) = ^true) {
        $crystal_furnace_count = add($crystal_furnace_count, 1);
    } 
    if(testbit(%legends_bits, ^legends_smelting_hunk) = ^true) {
        $crystal_furnace_count = add($crystal_furnace_count, 1);
    } 
    if(testbit(%legends_bits, ^legends_smelting_lump) = ^true) {
        $crystal_furnace_count = add($crystal_furnace_count, 1);
    }

    if($crystal_count >= 2) {
        $text = append($text, "|@dbl@I've found some @dre@crystals@dbl@, perhaps these will help?");
    } else if($crystal_count = 1) {
        $text = append($text, "|@dbl@I've found a @dre@crystal@dbl@, perhaps this will help?");
    }

    if($crystal_furnace_count >= 2) {
        $text = append($text, "|@dbl@I've place some @dre@crystal chunks@dbl@ in a @dre@lava furnace@dbl@.");
    } else if($crystal_furnace_count = 1) {
        $text = append($text, "|@dbl@I've placed a @dre@crystal@dbl@ chunk in a @dre@lava furnace@dbl@.");
    }
}

if(%legendsquest = ^legends_heart_in_recess) {
    $text = append($text, "|@str@I made a glowing dragon heart stone from chunks of @str@crystal which gave me access to a strange cave.");
} else if(%legendsquest = ^legends_crystal_pieces_smelted) {
    if(inv_total(inv, heartcrystal_glow) > 0) {
        $text = append($text, "|@dbl@I have a @dre@glowing dragon heart@dbl@ crystal which might help me @dbl@to gain access to the @dre@sacred water@dbl@.");
    } else if(inv_total(inv, heartcrystal) > 0) {
        $text = append($text, "|@dbl@I have a @dre@Dragon heart@dbl@ crystal which might help me @dbl@to gain @dbl@access to the @dre@sacred water@dbl@.");
    } else {
        $text = append($text, "|@dbl@I've lost the dragon heart crystal. I should try to get @dbl@more crystal chunks and make another dragon heart.");
    }
    $text = append($text, "|@dbl@I need to find the @dre@source@dbl@ of the @dre@sacred water@dbl@.");
}

if(%legendsquest = ^legends_pushed_boulder) {
    $text = append($text, "|@str@A strange spirit seems to be guarding the sacred water.");
} else if(%legendsquest = ^legends_heart_in_recess) {
    $text = append($text, "|@dbl@I need to find the source of the @dre@sacred water@dbl@ and collect @dbl@some so I can grow a @dre@Yommi tree@dbl@.");
}

if(%legendsquest >= ^legends_defeated_nezikchened_water & testbit(%legends_bits, ^legends_killed_viyeldi) = ^false) {
    // OSRS https://youtu.be/uYxkN3aJ-44?si=tqlI4SWhr7Z2yB1C&t=7469
    $text = append($text, "|@str@The spirit Echnid Zeikin gave me a dagger and asked me @str@to kill Viyeldi. The Spirit turned out to be a demon in @str@disguise. I wasn't tricked, though and I defeated the @str@demon with Ungadulu's help.");
} else if(%legendsquest >= ^legends_received_dagger) {
    $text = append($text, "|@str@The spirit gave me a dagger and said I have to kill Viyeldi @str@with it if I want to get some sacred water.");
}

if(%legendsquest = ^legends_received_dagger & testbit(%legends_bits, ^legends_killed_viyeldi) = ^true) {
    if(testbit(%legends_bits, ^legends_given_dagger) = ^true) {
        $text = append($text, "|@str@I killed Viyeldi with the black dagger and I returned it to @str@the spirit, but it turned into the demon.");
        $text = append($text, "|@dbl@I guess I'm going to have to defeat this @dre@demon@dbl@.");
    } else {
        $text = append($text, "|@str@I've killed Viyeldi with a dagger the spirit gave me.");
    }
}

if(%legendsquest >= ^legends_defeated_nezikchened_water & testbit(%legends_bits, ^legends_killed_viyeldi) = ^true) {
    $text = append($text, "|@str@I was tricked by the demon into killing Viyeldi but I @str@managed to destroy the demon in the end.");
}

if(%legendsquest = ^legends_received_dagger & testbit(%legends_bits, ^legends_given_dagger) = ^false) {
    if(testbit(%legends_bits, ^legends_killed_viyeldi) = ^true) $text = append($text, "|@dbl@The @dre@Spirit@dbl@ said I should return the @dre@dagger@dbl@ to him.");
    else $text = append($text, "|@dbl@The @dre@Spirit@dbl@ said I should return the @dre@dagger@dbl@ to him once I @dbl@had @dre@killed Viyeldi@dbl@.");
    if(~obj_gettotal(holyforce) > 0) {
        // OSRS https://github.com/Indio3/quest-journals/blob/master/quest_legends/2025-03-28_18-19-11.png?raw=true
        $text = append($text, "@dbl@I told @dre@Ungadulu@dbl@ about the @dre@spirit@dbl@. Ungadulu may be able to help me.");
    } else if(inv_total(inv, deathdaggerdone) > 0 | inv_total(worn, deathdaggerdone) > 0 | inv_total(inv, deathdagger) > 0 | inv_total(worn, deathdagger) > 0) {
        if(testbit(%legends_bits, ^legends_killed_viyeldi) = ^true) {
            $text = append($text, "|@dbl@I have the @dre@dagger@dbl@ with which I killed @dre@Viyeldi@dbl@.");
            $text = append($text, "|@dbl@The @dre@spirit@dbl@ asked me to return this.");
        } else {
            $text = append($text, "|@dbl@I have a death dagger from the @dre@spirit@dbl@, @dre@Echned Zekin@dbl@, the @dbl@spirit asked me to kill @dre@Viyeldi@dbl@.");
        }
    }
}

if(%legendsquest >= ^legends_spawned_nezikchened_final) {
    $text = append($text, "|@str@I made a Yommi tree totem pole by growing a Yommi @str@tree and watering it with sacred water.");
} else if(%legendsquest = ^legends_collected_totem) {
    $text = append($text, "|@str@I collected some sacred water in order to I made a totem @str@pole from a Yommi tree.");
} else if(%legendsquest = ^legends_sacred_water_collected) {
    $text = append($text, "|@str@I now have some pure sacred water.");
} else if(%legendsquest = ^legends_defeated_nezikchened_water) {
    $text = append($text, "|@str@I found the source of the sacred water!");
} else if(%legendsquest = ^legends_pushed_boulder | %legendsquest = ^legends_received_dagger) {
    $text = append($text, "|@dbl@I need to find a way to get some @dre@sacred water@dbl@.");
}

if(%legendsquest >= ^legends_spawned_nezikchened_final) {
    $text = append($text, "|@str@I used the totem pole on an evil totem pole and the demon @str@Nezikchened appeared again.");
} else if(%legendsquest = ^legends_collected_totem) {
    $text = append($text, "|@dbl@I've got a @dre@Yommi tree totem pole@dbl@, I need to place this @dbl@somewhere so that @dre@Gujuo@dbl@ can gather his people together. @dbl@Perhaps then I can make @dre@friends@dbl@ with the Kharazi tribe.");
} else if(%legendsquest = ^legends_sacred_water_collected) {
    $text = append($text, "|@dbl@I can now try to grow a @dre@Yommi tree@dbl@ in order to make a @dre@Yommi tree totem pole@dbl@.");
} else if(%legendsquest = ^legends_defeated_nezikchened_water) {
    $text = append($text, "|@dbl@I need to collect some @dre@sacred water@dbl@ in order to grow a @dre@Yommi tree@dbl@ and make it into a @dre@totem pole@dbl@.");
}


if(%legendsquest >= ^legends_defeated_nezikchened_final) {
    // OSRS holy force https://youtu.be/uYxkN3aJ-44?si=Z45LrN65aNgGhKLU&t=8026
    if(testbit(%legends_bits, ^legends_killed_viyeldi) = ^false) {
        $text = append($text, "|@str@I fought Nezikchened and finally defeated him.");
    } else {
        $text = append($text, "|@str@Nezikchened appeared and summoned the Viyeldi fighters.");
        $text = append($text, "|@str@I fought them all and eventually defeated Nezikchened.");
    }
} else if(%legendsquest = ^legends_spawned_nezikchened_final) {
    if(testbit(%legends_bits, ^legends_killed_viyeldi) = ^true & getbit_range(%legends_bits, ^legends_defeated_san_final, ^legends_defeated_ranalph_final) < 7) {
        $text = append($text, "|@dbl@I have to defeat the demon @dre@Nezikchened@dbl@ in order to @dbl@replace the @dre@evil totem pole.");
    } else {
        $text = append($text, "|@dbl@I have to defeat the demon @dre@Nezikchened@dbl@ and the @dre@Viyeldi @dre@fighters@dbl@ in order to replace the evil totem pole.");
    }
}


if(%legendsquest >= ^legends_got_gilded_totem) {
    $text = append($text, "|@str@I replaced the evil totem with the good one I made.");
} else if(%legendsquest >= ^legends_defeated_nezikchened_final) {
    if(inv_total(inv, thtotempole) > 0) {
        $text = append($text, "|@dbl@I need to replace the @dre@evil totem pole@dbl@ with the one I made.");
    } else {
        $text = append($text, "|@dbl@I don't have a totem pole, I need to make a new one.");
    }
}

if(%legendsquest > ^legends_got_gilded_totem) {
    $text = append($text, "|@str@After mapping the Kharazi Jungle for Radimus Erkle|@str@I freed the Kharazi tribe from the power a foul demon|@str@called Nezikchened.");
    $text = append($text, "|@str@In return for this heroic action the Kharazi tribe presented @str@me with a gilded totem pole.");
    $text = append($text, "|@str@I've handed the totem pole and map over to Radimus.");
} else if(%legendsquest = ^legends_got_gilded_totem) {
    if(~obj_gettotal(thtotempolegift) > 0) {
        $text = append($text, "|@str@Gujuo gave me a fine gilded totem pole for freeing the @str@Kharazi Jungle from Nezikchened's demonic power.");
        $text = append($text, "|@dbl@The @dre@gilded totem pole@dbl@ will make a find addition to the @dre@Legends Guild@dbl@.");
    } else {
        $text = append($text, "|@dbl@Perhaps @dre@Gujuo@dbl@ can call his people together now and I can @dbl@make @dre@friends@dbl@ with the @dre@Kharazi Jungle tribe@dbl@.");
    }
}

def_int $remaining = calc((^legends_radimus_training_4 - %legendsquest) / 5);
def_string $training_sessions;
if($remaining = 4) $training_sessions = "four";
else if ($remaining = 3) $training_sessions = "three";
else if ($remaining = 2) $training_sessions = "two";
else $training_sessions = "one";
def_string $session_left = "sessions";
if ($remaining = 1) {
    $session_left = "session";
}

def_int $completed = calc(4 - $remaining);
def_string $training_completed;
if($completed = 4) $training_completed = "four";
else if ($completed = 3) $training_completed = "three";
else if ($completed = 2) $training_completed = "two";
else $training_completed = "one";
def_string $session_completed = "sessions";
if ($completed = 1) {
    $session_completed = "session";
}

if(%legendsquest > ^legends_returned_to_radimus) {
    $text = append($text, "|@str@The gilded totem pole is on display in the Legends Guild @str@for all to see.");
    if($completed > 0) $text = append($text, "|@str@Radimus has given me <$training_completed> training <$session_completed> as reward.");
} else if(%legendsquest = ^legends_returned_to_radimus) {
    $text = append($text, "|@dre@Radimus@dbl@ asked me to join him inside the @dre@Legends Guild@dbl@ to @dbl@discuss my reward.");
}

if(%legendsquest > ^legends_returned_to_radimus) {
    $text = append($text, "|@dre@Radimus Erkle@dbl@, the @dre@Grand Vizier@dbl@ of the @dre@Legends Guild@dbl@ has @dbl@offered me some @dre@training@dbl@ for completing the @dre@quest@dbl@.");
    $text = append($text, "|@dbl@I still have @dre@<$training_sessions>@dbl@ training <$session_left> with @dre@Radimus@dbl@.");
}

~quest_journal("The Legends Quest", $text);
