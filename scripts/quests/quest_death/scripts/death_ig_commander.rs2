[opnpc1,death_ig_commander]
~chatplayer("<p,happy>Hello!");
switch_int(%death_equiproom) {
    case ^death_complete :
        ~chatnpc("<p,happy>Welcome back friend!");
        // TODO: Troll stronghold
    case ^death_not_started : 
        ~chatnpc("<p,happy>Hello citizen, how can I help?");
        @multi3("Do you have any quests for me?", denulth_quests, "What is this place?", denulth_place, "You can't, thanks.", denulth_youcant);
    case default :
        if(%death_equiproom = ^death_unlocked_door & ~death_get_map >= ^death_scouted_area) {
            @denulth_has_map;
        }
        if(~death_get_map < ^death_given_cert & ~death_get_map >= ^death_spoken_smithy) {
            @denulth_cert;
        }
        ~chatnpc("<p,happy>Hello citizen, is there anything you'd like to know?");
        @multi4("Can you remind me of the quest I am on?", denulth_remind, "I thought the White Knights controlled Asgarnia?", denulth_whiteknights, "What is this place?", denulth_place, "That's all, thanks.", denulth_thatsall);
}

[label,denulth_moreoptions]
switch_int(%death_equiproom) {
    case ^death_not_started : 
        ~chatnpc("<p,neutral>Can I assist you with anything else?");
        @multi3("Do you have any quests for me?", denulth_quests, "What is this place?", denulth_place, "You can't, thanks.", denulth_youcant);
    case default :
        ~chatnpc("<p,neutral>Is there anything else you would like to know?");
        @multi4("Can you remind me of the quest I am on?", denulth_remind, "I thought the White Knights controlled Asgarnia?", denulth_whiteknights, "What is this place?", denulth_place, "That's all, thanks.", denulth_thatsall);
}

[label,denulth_quests]
~chatplayer("<p,neutral>Do you have any quests for me?");
~chatnpc("<p,sad>I don't know if you can help us!");
// https://web.archive.org/web/20061231212357im_/http://www.runeweb.net/fireball/Death%20Plateau%20Images/DeathPlateau1.PNG
~chatnpc("<p,sad>The trolls have taken up camp on Death Plateau!|They are using it to launch raids at night on the village.|We have tried to attack the camp|but the main path is heavily guarded!");
~chatplayer("<p,neutral>Perhaps there is a way you can sneak up at night?");
~chatnpc("<p,sad>If there is another way I do not know of it.");
~chatnpc("<p,confused>Do you know of such a path?");
switch_int(~p_choice2("No but perhaps I could try and find one?", 1, "No, sorry.", 2)) {
    case 1 :
        ~chatplayer("<p,happy>No but perhaps I could try and find one?");
        ~chatnpc("<p,happy>Citizen you would be well rewarded!");
        ~chatnpc("<p,neutral>If you go up to Death Plateau be very careful as the trolls will attack you on sight!");
        ~chatplayer("<p,neutral>I'll be careful.");
        ~chatnpc("<p,neutral>One other thing.");
        ~chatplayer("<p,neutral>What's that?");
        ~chatnpc("<p,neutral>All of our equipment is kept in the castle on the hill.");
        // https://web.archive.org/web/20061231212357im_/http://www.runeweb.net/fireball/Death%20Plateau%20Images/DeathPlateau2.PNG
        ~chatnpc("<p,angry>The stupid guard that was on duty last|night lost the combination to the lock!|I told the Prince that the Imperial Guard|should've been in charge of security!");
        ~chatplayer("<p,neutral>No problem, what does the combination look like?");
        ~chatnpc("<p,neutral>The equipment room is unlocked when the stone balls are placed in the correct order on the stone mechanism outside it. The right order is written on a piece of paper the guard had.");
        ~chatplayer("<p,confused>A stone what...?!");
        %death_equiproom = ^death_started;
        ~death_set_haroldgold(100);
        ~send_quest_progress_colour(questlist:death, %death_equiproom, ^death_complete);
        ~chatnpc("<p,angry>Well citizen, the Prince is fond of puzzles. Why we couldn't just have a key is beyond me!");
        ~chatplayer("<p,neutral>I'll get on it right away!");
    case 2 : 
        ~chatplayer("<p,neutral>No, sorry.");
        ~chatnpc("<p,sad>Never mind citizen.");
        @denulth_moreoptions;
}

[label,denulth_remind]
~chatplayer("<p,confused>Can you remind me of the quest I am on?");
~chatnpc("<p,neutral>You offered to see if you could find another way up Death Plateau. We could then use it to sneak up and attack the trolls by night.");
~chatplayer("<p,neutral>Ah yes, and the guard had lost the combination to your equipment room in the castle on the hill?");
~chatnpc("<p,neutral>That's right citizen, you offered to recover the combination and unlock the door.");
if(%death_equiproom = ^death_unlocked_door) {
    ~chatplayer("<p,happy>I've unlocked the equipment room so I just need to find an alternate route up Death Plateau!");
    ~chatnpc("<p,happy>Good work citizen!");
} else if (~death_get_map = ^death_scouted_area) {
    ~chatplayer("<p,happy>I've found a secret way up Death Plateau so I just need to find the combination and unlock the door.");
    ~chatnpc("<p,happy>Good work citizen!");
}
@denulth_moreoptions;

[label,denulth_place]
~chatplayer("<p,neutral>What is this place?");
~chatnpc("<p,happy>Welcome to the Principality of Burthorpe!");
~chatnpc("<p,neutral>We are the Imperial Guard for his Royal Highness Prince Anlaf of Burthorpe.");
@denulth_moreoptions;

[label,denulth_whiteknights]
~chatplayer("<p,confused>I thought the White Knights controlled Asgarnia?");
~chatnpc("<p,sad>You are right citizen. The White Knights have taken advantage of the old and weak king, they control most of Asgarnia, including Falador. However they do not control Burthorpe!");
~chatnpc("<p,angry>We are the prince's elite troops! We keep Burthorpe secure!");
~chatnpc("<p,sad>The White Knights have overlooked us, until now! They are pouring money into their war against the Black Knights, they are looking for an excuse to stop our funding and I'm afraid they may have found it!");
~chatnpc("<p,sad>If we can not destroy the troll camp on Death Plateau then the Imperial Guard will be disbanded and Burthorpe will come under control of the White Knights. We can not let this happen!");
@denulth_moreoptions;

[label,denulth_youcant]
~chatplayer("<p,neutral>You can't, thanks.");

[label,denulth_thatsall]
~chatplayer("<p,neutral>That's all, thanks.");
~chatnpc("<p,neutral>God speed citizen.");

[label,denulth_cert]
~chatnpc("<p,neutral>Hello citizen, have you found another way up Death Plateau?");
if(~death_get_map = ^death_got_entrancecert) {
    if(~obj_gettotal(death_entrancecert) = 0) {
        ~chatplayer("<p,neutral>I'm working on it but I've lost the certificate!");
        ~chatnpc("<p,neutral>No problem, I have a duplicate.");
        inv_add(inv, death_entrancecert, 1);
        mes("Denulth has given you a certificate.");
        ~objbox(death_entrancecert, "Denulth has given you a certificate.", 250, 0, 0);
        return;
    }
    ~chatplayer("<p,happy>I'm working on it, I just need to give the certificate to Dunstan.");
    return;
}
~chatplayer("<p,happy>Yes there is another way up Death Plateau!");
~chatnpc("<p,happy>We are saved!");
~chatplayer("<p,neutral>There's one thing...");
~chatnpc("<p,confused>And what is that citizen?");
~chatplayer("<p,confused>There is a Sherpa who will only show me the secret way if I first get some spikes for his climbing boots. The smith will only do this for me if you sign up his son for the Imperial Guard!");
~chatnpc("<p,neutral>Hmm...this is very irregular.");
~chatplayer("<p,sad>Will you not do this?");
~chatnpc("<p,neutral>I have heard of Dunstan's son, he is a very promising young man. For the sake of your mission we can make an exception!");
~death_set_map(^death_got_entrancecert);
inv_add(inv, death_entrancecert, 1);
mes("Denulth has given you a certificate.");
~objbox(death_entrancecert, "Denulth has given you a certificate.", 250, 0, 0);
~chatnpc("<p,neutral>This certificate proves that we have accepted Dunstan's son for training in the Imperial Guard!");
~chatplayer("<p,happy>Thank you Denulth, I shall be back shortly!");

[label,denulth_has_map]
if(testbit(%death_bits, ^denulth_given_map) = ^true) {
    ~chatnpc("<p,neutral>Hello citzen, I have the map of the secret way you gave me earlier.");
} else {
    ~chatnpc("<p,neutral>Hello citizen, have you found another way up Death Plateau?");
    ~chatplayer("<p,happy>Yes! There is a path that runs from a Sherpa's hut around the back of Death Plateau. The trolls haven't found it yet. The Sherpa made a map I can give you.");
    if(inv_total(inv, death_secretwaymap) = 0) {
        ~chatplayer("<p,neutral>I don't have the map on me.");
    } else {
        mes("You give Denulth the map of the secret way.");
        %death_bits = setbit(%death_bits, ^denulth_given_map);
        inv_del(inv, death_secretwaymap, 1);
        ~objbox(death_secretwaymap, "You give Denulth the map of the secret way.", 250, 0, divide(^objbox_height, 2));
        ~chatnpc("<p,happy>Excellent, this looks perfect. They will never see us coming.");
    }
}
if(testbit(%death_bits, ^denulth_given_combo) = ^true) {
    ~chatnpc("<p,neutral>I have the combination to the equipment room that you recovered for me.");
} else {
    ~chatnpc("<p,neutral>Have you managed to open the equipment room?");
    if(inv_total(inv, death_combination) = 0) {
        ~chatplayer("<p,neutral>I have opened the door but I don't have the combination on me.");
    } else {
        ~chatplayer("<p,neutral>Yes! The door is open and here is the combination.");
        mes("You give Denulth the combination to the equipment room.");
        %death_bits = setbit(%death_bits, ^denulth_given_combo);
        inv_del(inv, death_combination, 1);
        ~objbox(death_combination, "You give Denulth the combination to the equipment room.", 250, 0, divide(^objbox_height, 2));
    }
}
if(testbit(%death_bits, ^denulth_given_combo) = ^true & testbit(%death_bits, ^denulth_given_map) = ^true) {
    queue(death_quest_complete, 0);
    ~chatnpc("<p,happy>Well done citizen! We will reward you by training you in attack!");
    ~chatnpc("<p,happy>I shall present you with some steel fighting claws. In addition I shall show you the knowledge of creating the fighting claws for yourself.");
    ~chatnpc("<p,happy>You are now an honorary member of the Imperial Guard!");
}