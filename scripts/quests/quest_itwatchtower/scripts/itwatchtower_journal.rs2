[if_button,questlist:itwatchtower]
def_string $n = "Watch Tower";
def_string $x;

if (%itwatchtower = ^itwatchtower_not_started) {
    $x = append($x, "@dbl@I can start this quest by speaking to the @dre@Watchtower|");
    $x = append($x, "@dre@wizard@dbl@ on the top floor of Yanille's @dre@Watchtower@dbl@.||");
    $x = append($x, "@dbl@To complete this quest I need:|");
    if (stat(magic) < 14) {
        $x = append($x, "@dre@Level 14 Magic|");
    } else {
        $x = append($x, "@str@Level 14 Magic|");
    }
    if (stat(mining) < 40) {
        $x = append($x, "@dre@Level 40 Mining|");
    } else {
        $x = append($x, "@str@Level 40 Mining|");
    }
    if (stat(herblore) < 14) {
        $x = append($x, "@dre@Level 14 Herblore|");
    } else {
        $x = append($x, "@str@Level 14 Herblore|");
    }
    if (stat(thieving) < 15) {
        $x = append($x, "@dre@Level 15 Thieving|");
    } else {
        $x = append($x, "@str@Level 15 Thieving|");
    }
    if (stat(agility) < 25) {
        $x = append($x, "@dre@Level 25 Agility|");
    } else {
        $x = append($x, "@str@Level 25 Agility|");
    }

    //Intentionally duplicated here
    if (testbit(%itwatchtower_bits, ^itwatchtower_looking_relic) = ^true) {
        $x = append($x, "@dbl@The north west guard wants a @dre@sign of friendship.|");
    }
    if (getbit_range(%itwatchtower_bits, ^itwatchtower_market_lower, ^itwatchtower_market_upper) = 1) {
        $x = append($x, "@dbl@The north-east guard wants @dre@a surprise gift.|");
    }
    ~quest_journal($n, $x);
    return;
}

if (%itwatchtower = ^itwatchtower_started) {
    $x = append($x, "@dbl@I accepted the challenge of finding the lost @dre@crystals.|");
    if(inv_total(inv, fingernails) > 0) {
        $x = append($x, "@dbl@I found some @dre@fingernails@dbl@ as evidence. I should take them to|");
        $x = append($x, "@dbl@the @dre@Watchtower wizard@dbl@ in the Watchtower.");
    } else {
        $x = append($x, "@dbl@I need to @dre@find evidence@dbl@ of what's happened. It has been|");
        $x = append($x, "@dbl@suggested I try searching the @dre@bushes@dbl@ near the|");
        $x = append($x, "@dbl@Watchtower.");
    }
    ~quest_journal($n, $x);
    return;
}

$x = append($x, "@str@I accepted the challenge of finding the lost crystals.|");
$x = append($x, "@str@I found some fingernails as evidence. I should take them to|");
$x = append($x, "@str@the Watchtower wizard.|");
if ((%itwatchtower >= ^itwatchtower_given_fingernails) & (%itwatchtower <= ^itwatchtower_given_riddle)) {
    $x = append($x, "|@dbl@Now I need to @dre@deal with the tribal ogres.||");
    $x = append($x, "@dbl@I should search the area to see if I can find any ogre and|");
    $x = append($x, "@dbl@try and communicate with the leaders.|");
    if (testbit(%itwatchtower_bits, ^itwatchtower_helped_og) = ^true) {
        $x = append($x, "@str@I returned Og's stolen gold.|");
    } else if (testbit(%itwatchtower_bits, ^itwatchtower_spoken_og) = ^true) {
        if(inv_total(inv, stolen_gold) > 0) {
            $x = append($x, "@dbl@I have Og's @dre@stolen gold@dbl@; I should give it to @dre@Og@dbl@ for a|");
            $x = append($x, "@dbl@reward.|");
        } else {
            $x = append($x, "@dbl@Og wants me to @dre@retrieve his stolen gold@dbl@ from Toban.|");
        }
    }
    if (testbit(%itwatchtower_bits, ^itwatchtower_helped_grew) = ^true) {
        $x = append($x, "@str@I knocked out one of Gorad's teeth and gave it to Grew.|");
    } else if (testbit(%itwatchtower_bits, ^itwatchtower_spoken_grew) = ^true) {
        if (inv_total(inv, ogretooth) > 0) {
            $x = append($x, "@dbl@I have @dre@one of Gorad's teeth@dbl@; I should give it to @dre@Grew@dbl@ for a|");
            $x = append($x, "@dbl@reward.|");
        } else {
            $x = append($x, "@dbl@Grew wants me to give him @dre@one of Gorad's teeth.|");
        }
    }
    if (testbit(%itwatchtower_bits, ^itwatchtower_helped_toban) = ^true) {
        $x = append($x, "@str@I gave the bones to Toban.|");
    } else if (testbit(%itwatchtower_bits, ^itwatchtower_spoken_toban) = ^true) {
        if (inv_total(inv, dragon_bones) > 0) {
            $x = append($x, "@dbl@I have the @dre@dragon bones@dbl@; I should give them to @dre@Toban@dbl@ for a|");
            $x = append($x, "@dbl@reward.|");
        } else {
            $x = append($x, "@dbl@Toban wants the @dre@bones of an adult dragon.|");
        }
    }
    if (%itwatchtower >= ^itwatchtower_given_relic) {
        $x = append($x, "@str@I gave the ogre relic to the north west guard.|");
    } else if (testbit(%itwatchtower_bits, ^itwatchtower_looking_relic) = ^true) {
        $x = append($x, "@dbl@The north west guard wants a @dre@sign of friendship.|");
        if (inv_total(inv, ogrerelic) > 0) {
            $x = append($x, "@dbl@I wonder if @dbl@this @dre@relic@dbl@ will do...|");
        }
    }
    if (getbit_range(%itwatchtower_bits, ^itwatchtower_market_lower, ^itwatchtower_market_upper) = 1) {
        $x = append($x, "@dbl@The north-east guard wants @dre@a surprise gift.|");
        if (inv_total(inv, rockcake) > 0) {
            $x = append($x, "@dbl@I have a @dre@rock cake@dbl@ that is very surprising.|");
            $x = append($x, "@dbl@I wonder if it will do...|");
        }
    } else if (getbit_range(%itwatchtower_bits, ^itwatchtower_market_lower, ^itwatchtower_market_upper) = 2) {
        $x = append($x, "@str@I gave the north-east guard a rock cake.|");
    }
    if (%itwatchtower = ^itwatchtower_given_riddle) {
        $x = append($x, "@dbl@Some guards gave me a @dre@puzzle to solve.|");
    }
    ~quest_journal($n, $x);
    return;
}

if (%itwatchtower = ^itwatchtower_solved_riddle) {
    $x = append($x, "|@str@I have solved the guard's puzzle.|");
    $x = append($x, "@dbl@I was given @dre@a map@dbl@ by the guard.|");
    if (inv_total(inv, skavidmap) > 0) {
        $x = append($x, "@dbl@I have it with me now, so I can navigate the skavid caves.|");
    } else {
        $x = append($x, "@dbl@I do not have the map with me now, so I cannot navigate|");
        $x = append($x, "@dbl@the caves.|");
    }
    $x = append($x, "@dbl@I need to @dre@search the skavid caves.|");
    if (testbit(%itwatchtower_bits, ^itwatchtower_learning_skavid) = ^true) {
        $x = append($x, "|@dbl@I have been taught a few words of the skavid language:|");
        $x = append($x, "|@dbl@ar, nod, gor, ig, cur|");
    }
    if (testbit(%itwatchtower_bits, ^itwatchtower_learned_ig) = ^true) {
        $x = append($x, "|@dre@'Cur bidith' - 'Ig'|");
    }
    if (testbit(%itwatchtower_bits, ^itwatchtower_learned_ar) = ^true) {
        $x = append($x, "|@dre@'Gor cur' - 'Ar'|");
    }
    if (testbit(%itwatchtower_bits, ^itwatchtower_learned_cur) = ^true) {
        $x = append($x, "|@dre@'Bidith tanath' - 'Cur'|");
    }
    if (testbit(%itwatchtower_bits, ^itwatchtower_learned_nod) = ^true) {
        $x = append($x, "|@dre@'Gor nod' - 'Nod'/'Tanath'|");
    }
    ~quest_journal($n, $x);
    return;
}

$x = append($x, "|@str@I was given a map by the guard.|");
$x = append($x, "@str@I need to search the skavid caves.|");
$x = append($x, "|@str@I found my way into the skavid caves.|");
if (%itwatchtower = ^itwatchtower_skavid_crystal) {//RS3
    $x = append($x, "@dbl@I have been told that the other crystals are in the|");
    $x = append($x, "@dre@shamans' enclave@dbl@. I should speak to the @dre@Watchtower|");
    $x = append($x, "@dre@wizard@dbl@ to find out how to get in.|");
    ~quest_journal($n, $x);
    return;
}
$x = append($x, "@str@I used some cave nightshade to distract the guard.|");
if (%itwatchtower = ^itwatchtower_fed_nightshade) {
    $x = append($x, "@dbl@I need to @dre@defeat the ogre shamans@dbl@ and @dre@find the other|");
    $x = append($x, "@dre@crystals.|");
    ~quest_journal($n, $x);
    return;
}

$x = append($x, "|@str@I need to defeat the ogre shamans and find the other|");
$x = append($x, "@str@crystals.|");
$x = append($x, "@str@I tried to defeat the shamans, but they are protected|");
$x = append($x, "@str@by powerful magics! I should speak with the Watchtower|");
$x = append($x, "@str@wizard to see if he has any advice for me.|");
if (%itwatchtower = ^itwatchtower_learned_potion) {
    if (inv_total(inv, ogre_potion) > 0) {
        $x = append($x, "|@dbl@I have made the @dre@ogre potion@dbl@. I need to get it enchanted|");
        $x = append($x, "@dbl@by the Watchtower wizard.");
    } else {
        $x = append($x, "@dbl@I need to make the @dre@potion@dbl@ that will @dre@defeat the ogre|");//guessed linebreak
        $x = append($x, "@dre@shamans@dbl@.");
    }
    ~quest_journal($n, $x);
    return;
}

if (%itwatchtower = ^itwatchtower_made_potion) {
    $x = append($x, "|@str@I have made the ogre potion.|");
    $x = append($x, "@str@I gave the potion to the wizard.|");
    $x = append($x, "@str@He infused it into a magic ogre potion.|");
    $x = append($x, "@str@I need to defeat the ogre shamans.|");
    def_int $current_kills = getbit_range(%itwatchtower_bits, ^itwatchtower_shaman_kills_lower, ^itwatchtower_shaman_kills_upper);
    if ($current_kills < 6) {
        $x = append($x, "|@dbl@Now I need to @dre@kill <tostring(calc(6 - $current_kills))> ogre shaman(s).|");
    } else {
        $x = append($x, "|@str@I killed all the ogre shamans.||");
        //this doesn't match rs3& osrs, even if you destroy the crystal the text says you have mined it
        if (inv_total(inv, powering_crystal4) > 0) {
            $x = append($x, "@dbl@I have @dre@mined the sacred rock@dbl@ and have taken the last|");
            $x = append($x, "@dre@crystal.|");
            if (~watchtower_has_all_crystals = true) {
                $x = append($x, "@dbl@I need to @dre@return all the crystals to the Watchtower wizard.|");
                $x = append($x, "@dre@|");
            } else {
                $x = append($x, "@dbl@I need to @dre@recover some of the crystals@dbl@ and take them|");
                $x = append($x, "@dbl@to the Watchtower wizard.|");
            }
        } else {
            $x = append($x, "@dbl@Now the @dre@shamans@dbl@ are dead, I should be able to|");
            $x = append($x, "@dre@mine the Rock of Dalgroth@dbl@.|");
        }
    }
    ~quest_journal($n, $x);
    return;
}

$x = append($x, "|@str@I need to return all the crystals to the Watchtower wizard.|");
if (%itwatchtower = ^itwatchtower_found_all_crystals) {
    $x = append($x, "|@dbl@I have taken the crystals to the Watchtower wizard. Now I|");
    $x = append($x, "@dbl@need to throw the @dre@lever@dbl@ to @dre@activate the shield@dbl@.|");
    //no placing crystals in 2004 like rs3/osrs show
    ~quest_journal($n, $x);
    return;
}

if (%itwatchtower >= ^itwatchtower_complete) {
    $x = append($x, "|@str@I helped the Wizards in Yanille, and rescued the|");
    $x = append($x, "@str@lost crystals.|");
    $x = append($x, "@str@I defeated the ogres and their minions.|");
    $x = append($x, "@str@My task here is done.|");
    $x = append($x, "|@dre@QUEST COMPLETE!|");
}

if (%itwatchtower < ^itwatchtower_complete_read_scroll) {
    $x = append($x, "|@dbl@I have been given a @dre@spell scroll@dbl@ as a reward, which I need to|");
    $x = append($x, "@dbl@ read.");
}

~quest_journal($n, $x);
