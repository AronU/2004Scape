[if_button,questlist:imp]
def_string $text;

if(%imp = ^imp_not_started) {
    $text = "@dbl@I can start this quest by talking to @dre@Wizard Mizgog@dbl@ in the|@dre@Wizards' Tower@dbl@.";
} else if(%imp = ^imp_started) {
    // Using manpaint's sources as reference
    $text = "@str@I have spoken to Wizard Mizgog.||";

    if(inv_total(inv, black_bead) > 0 & inv_total(inv, red_bead) > 0 & inv_total(inv, white_bead) > 0 & inv_total(inv, yellow_bead) > 0) {
        $text = append($text, "@dbl@I have collected all the missing beads and need to return|@dbl@them to @dre@Wizard Mizgog@dbl@.");
    } else {
        $text = append($text, "@dbl@I need to collect some items by killing @dre@Imps@dbl@, I can find|@dbl@Imps all over the kingdom.|");
        if(inv_total(inv, black_bead) > 0) {
            $text = append($text, "@str@1 Black Bead.|");
        } else {
            $text = append($text, "@dre@1 Black Bead.|");
        }

        if(inv_total(inv, red_bead) > 0) {
            $text = append($text, "@str@1 Red Bead.|");
        } else {
            $text = append($text, "@dre@1 Red Bead.|");
        }

        if(inv_total(inv, white_bead) > 0) {
            $text = append($text, "@str@1 White Bead.|");
        } else {
            $text = append($text, "@dre@1 White Bead.|");
        }

        if(inv_total(inv, yellow_bead) > 0) {
            $text = append($text, "@str@1 Yellow Bead.|");
        } else {
            $text = append($text, "@dre@1 Yellow Bead.|");
        }
    }
} else {
    $text = "@str@I have spoken to Wizard Mizgog.||@str@I have collected all the beads.||@str@Wizard Mizgog thanked me for finding his beads and gave|@str@me an Amulet of Accuracy.|@red@QUEST COMPLETE!";
}

~quest_journal("Imp Catcher", $text);
