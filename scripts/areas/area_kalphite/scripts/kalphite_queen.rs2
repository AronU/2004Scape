[ai_queue1,kalphite_queen]
if (random(^kalphite_worker_spawn_rate) = 0 & npc_stat(hitpoints) < 175) { // doesnt spawn until boss is under 175 ish hp https://youtu.be/ZHaxxMq3nVI?list=PLn23LiLYLb1YiomJ3wnXokdbgjE7-Lgt5&t=87
    ~kalphite_worker_spawn;
}
~npc_default_retaliate_ap;

[ai_opplayer2,kalphite_queen] npc_setmode(applayer2);

[ai_applayer2,kalphite_queen]
if (%npc_action_delay > map_clock) {
    return;
}
if (~npc_check_notcombat = false) {
    npc_setmode(null);
    return;
}
if (~npc_check_notcombat_self = false) {
    return;
}
// def_int $random = random(2);
// if (npc_range(coord) <= 1) {
//     $random = random(3);
// }
// switch_int($random) {
//     case 0 : ~kalphite_queen_mage_attack;
//     case 1 : ~kalphite_queen_ranged_attack;
//     case 2 : ~kalphite_melee_attack;
// }
def_int $random = 0;
if (npc_range(coord) <= 1) {
    $random = random(2);
}
switch_int($random) {
    case 0 : ~kalphite_queen_mage_attack;
    case 1 : ~kalphite_melee_attack;
}

[ai_queue3,kalphite_queen]
npc_walk(npc_coord);
npc_setmode(none);
npc_arrivedelay;
.npc_add(npc_coord, kalphite_larva, 5); // kalphite queen corpse
.npc_anim(kalphite_crackopen, 0);
~sound_area(kalphite_crack_open, 0, movecoord(npc_coord, 2, 0, 2), 12);
npc_changetype_keepall(kalphite_flyingqueen, 2000); // 20 min in osrs
npc_statheal(hitpoints, 0, 100);
// https://x.com/JagexAsh/status/861503532712722432 doesnt stat heal cmb stats
npc_anim(kalphite_queen_rising, 0);
npc_delay(1);
npc_sethuntmode(kalphite_queen);
.npc_del;

[proc,kalphite_queen_mage_attack]
spotanim_npc(kalphite_glow, 0, 0);
npc_anim(kalphite_lightning, 0);
sound_synth(kalthite_lightning, 0, 30);
if_close;
%npc_action_delay = add(map_clock, npc_param(attackrate));

~kalphite_queen_mage_huntall;

// coding it this way makes it so the coords of each target is stored instantly
// this means itll look really wonky if players are moving around... Which seems to match old vids ? :)
// - https://youtu.be/XT8ypzIIOE8?list=PLn23LiLYLb1YiomJ3wnXokdbgjE7-Lgt5&t=9

// additionally, the huntall coord is the initial target:
// - https://youtu.be/78BFTtWOKqU?list=PLn23LiLYLb1YiomJ3wnXokdbgjE7-Lgt5&t=36

// also additionally, first target and 2nd target are ALWAYS same tick, no matter the distance
// - https://youtu.be/euEfNVISvbo?list=PLn23LiLYLb1YiomJ3wnXokdbgjE7-Lgt5&t=52

// one more thing! first target, the spotanim is after the projectile hits, all other targets the spotanim is after the previous projectile hits 
// - see any of the vids above, or any vid of kq ever

// okay one more thing i swear, damage delays are so weird lol the only way ive been able to replicate it is by adding
// up the total distance traveled and dividing it by 5, instead of the conventional divide($duration, 30).
// This video shows the 3rd target has inconsistent damage delays?! what is this about? Is it lag or
// - https://youtu.be/3d0YtI59i70?t=110

// Last one forreal this time, the target order is just standard huntall :) yippee
[proc,kalphite_queen_mage_huntall]
def_coord $prev_coord = movecoord(npc_coord, 2, 0, 2);
def_int $duration = 0;
def_int $total_distance = 0;
huntall(coord, 10, 1);
while (huntnext = true) {
    $prev_coord, $duration, $total_distance = ~kalphite_queen_mage_hunt($prev_coord, $duration, $total_distance);
}

[proc,kalphite_queen_mage_hunt](coord $prev_coord, int $duration, int $total_distance)(coord, int, int)
def_int $damage = 0;
def_int $maxhit = 31;
if (
    randominc(~npc_magic_attack_roll) > randominc(~player_defence_roll_specific(^magic_style)) &
    ~check_protect_prayer(^magic_style) = false
) {
    $damage = randominc($maxhit);
}
~npc_set_attack_vars;

def_int $queue_delay = 0;
if ($duration = 0) { // first target
    $duration = ~player_projectile($prev_coord, coord, uid, kalphite_glow_travel, 40, 36, $duration, 15, 0, 11, 5);
    spotanim_pl(kalphite_glow_impact, 124, $duration);
    sound_synth(kalthite_lightning_impact, 0, $duration);
    $total_distance = add(add($total_distance, distance(coord, $prev_coord)), 1);
    $queue_delay = divide($total_distance, 5);
} else {
    spotanim_pl(kalphite_glow_impact, 124, $duration);
    sound_synth(kalthite_lightning_impact, 0, $duration);
    $duration = ~player_projectile($prev_coord, coord, uid, kalphite_glow_travel, 36, 36, $duration, 0, 0, 11, 5);
    $queue_delay = divide($total_distance, 5);
    $total_distance = add(add($total_distance, distance(coord, $prev_coord)), 1);
}
// mes("Distance: <tostring($total_distance)>, delay: <tostring($queue_delay)>");
anim(%com_defendanim, $duration);
queue(combat_damage_player, $queue_delay, $damage);
queue*(playerhit_n_retaliate, $queue_delay)(npc_uid);
return(coord, $duration, $total_distance);

// osrs, i dont think this matches old videos tbh:
// distance of 8: map_projanim_v2	id=kalphite_spine (288), starttime=30, endtime=74, angle=20, progress=255, startheight=120, endheight=124
// distance of 6: map_projanim_v2	id=kalphite_spine (288), starttime=30, endtime=58, angle=20, progress=255, startheight=120, endheight=124
// [proc,kalphite_queen_ranged_attack]
// if_close;
// npc_anim(kalphite_ranged_attack, 0);
// %npc_action_delay = add(map_clock, npc_param(attackrate));

// def_int $damage = 0;
// def_int $maxhit = 31;
// def_int $duration = 0;
// def_int $i = 0;
// huntall(coord, 5, 1);
// while (huntnext = true & $i < 5) { // up to 5? https://youtu.be/ZHaxxMq3nVI?list=PLn23LiLYLb1YiomJ3wnXokdbgjE7-Lgt5&t=48
//     $damage = 0;
//     if (randominc(~npc_ranged_attack_roll) > randominc(~player_defence_roll_specific(^ranged_style))) $damage = randominc($maxhit);
//     ~npc_set_attack_vars;
    
//     $duration = ~player_projectile(movecoord(npc_coord, 2, 0, 2), coord, uid, kalphite_spine, 40, 36, 0, 15, 0, 11, 5);
//     queue(combat_damage_player, divide($duration, 30), $damage);
//     queue*(playerhit_n_retaliate, divide($duration, 30))(npc_uid);
//     sound_synth(kalphite_queen_spines, 0, 0);
//     $i = add($i, 1);
// }

[ai_queue1,kalphite_flyingqueen]
if (random(^kalphite_worker_spawn_rate) = 0) {
    ~kalphite_worker_spawn;
}
~npc_default_retaliate_ap;

[ai_applayer2,kalphite_flyingqueen]
if (%npc_action_delay > map_clock) {
    return;
}
if (~npc_check_notcombat = false) {
    npc_setmode(null);
    return;
}
if (~npc_check_notcombat_self = false) {
    return;
}
// def_int $random = random(2);
// if (npc_range(coord) <= 1) {
//     $random = random(3);
// }
// switch_int($random) {
//     case 0 : ~kalphite_flyingqueen_mage_attack;
//     case 1 : ~kalphite_flyingqueen_ranged_attack;
//     case 2 : ~kalphite_flyingqueen_melee_attack;
// }
def_int $random = 0;
if (npc_range(coord) <= 1) {
    $random = random(2);
}
switch_int($random) {
    case 0 : ~kalphite_flyingqueen_mage_attack;
    case 1 : ~kalphite_flyingqueen_melee_attack;
}

[ai_opplayer2,kalphite_flyingqueen] npc_setmode(applayer2);

[proc,kalphite_flyingqueen_melee_attack]
anim(%com_defendanim, 0);
if (random(2) = 0) {
    npc_anim(kalphite_queen_attack_mandibles, 0);
} else {
    npc_anim(kalphite_queen_attack_claws, 0);
}
if (npc_param(attack_sound) ! null) {
    sound_synth(npc_param(attack_sound), 0, 0);
}
~npc_meleeattack;

[proc,kalphite_flyingqueen_mage_attack]
spotanim_npc(kalphite_queen_glow, 0, 0);
npc_anim(kalphite_queen_lightning, 0);
sound_synth(kalthite_lightning, 0, 30);

if_close;
%npc_action_delay = add(map_clock, npc_param(attackrate));

~kalphite_queen_mage_huntall;

// [proc,kalphite_flyingqueen_ranged_attack]
// if_close;
// npc_anim(kalphite_queen_ranged_attack, 0);
// %npc_action_delay = add(map_clock, npc_param(attackrate));

// def_int $damage = 0;
// def_int $duration = 0;
// def_int $maxhit = 31;
// def_int $i = 0;
// huntall(coord, 5, 1);
// while (huntnext = true & $i < 5) { // up to 5? https://youtu.be/ZHaxxMq3nVI?list=PLn23LiLYLb1YiomJ3wnXokdbgjE7-Lgt5&t=48
//     $duration = ~player_projectile(movecoord(npc_coord, 2, 0, 2), coord, uid, kalphite_queen_spine, 40, 36, 0, 15, 0, 11, 5);
//     $damage = 0;
//     if (randominc(~npc_ranged_attack_roll) > randominc(~player_defence_roll_specific(^ranged_style))) {
//         $damage = randominc($maxhit);
//     }
//     queue(combat_damage_player, divide($duration, 30), $damage);
//     queue*(playerhit_n_retaliate, divide($duration, 30))(npc_uid);
//     ~npc_set_attack_vars;
//     sound_synth(kalphite_queen_spines, 0, 0);
//     $i = add($i, 1);
// }
