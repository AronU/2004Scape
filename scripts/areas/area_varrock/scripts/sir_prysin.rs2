[opnpc1,sir_prysin]
if(map_members = ^true & inv_total(inv, trail_clue_hard_riddle015) > 0) {
    ~chatnpc("<p,happy>You've come to the right place, well done!");
    ~progress_clue_hard(trail_clue_hard_riddle015, "Sir Prysin has given you another clue scroll!");
    return;
}
if (%demonstart = ^demon_not_started | %demonstart = ^demon_talked_aris) {
    @demon_slayer_sir_prysin_pre_silverlight;
} else if (%demonstart >= ^demon_key_hunt & %demonstart < ^demon_silverlight) {
    @demon_slayer_sir_prysin_key_search_progress;
} else if (%demonstart = ^demon_silverlight){
    @demon_slayer_need_to_kill;
} else if (%demonstart = ^demon_complete) {
    if (%demon2start = ^demon2_talked_aris) {
        @demon_slayer_2_ask_about_demon;
    }  else if (%demon2start = ^demon2_defeated_acolyte_3) {
        @demon_slayer_2_imbue_silverlight;
    } else if (%demon2start = ^demon2_ashes_combined) {
        @demon_slayer_2_where_agrith_vhann_is;
    } else if (%demon2start = ^demon2_complete) {
        @demon_slayer_2_completed;
    } else {
    @demon_slayer_completed;
    }
}

[opnpcu,sir_prysin]
if (last_useitem = zamorak_scroll) {
    ~chatplayer("<p,neutral>I found this scroll hidden in the Zamorak church.");
    ~chatnpc("<p,quiz>...That's Zamorakian scripture, no doubt about it. Charred edges, ritual phrasing... this wasn't meant for wandering eyes.");
    ~chatplayer("<p,neutral>It talks about an altar near molten stone.");
    ~chatnpc("<p,neutral>Hmm. Zamorakians favour places already touched by chaos, lava flows, demon-haunted ground... that sort of thing. They believe such locations 'remember' past rituals.");
    ~chatplayer("<p,quiz>Does anything like that come to mind?");
    ~chatnpc("<p,neutral>That doesn't sound like any place in Misthalin. You should explore west of here. If the scroll is truthful, the cult will have chosen a place steeped in flame and history.");
    ~chatplayer("<p,neutral>Right. I'll start searching.");
    ~chatnpc("<p,neutral>Best of luck to you adventurer.");
    return;
}
~displaymessage(^dm_default);

[label,demon_slayer_2_imbue_silverlight]
~chatplayer("<p,neutral>I've gathered the ashes from the three infernal flames.");
~chatnpc("<p,quiz>You actually did it...");
~chatplayer("<p,quiz>Can Silverlight be strengthened with these?");
if (~demon_slayer_2_check_if_has_ashes = true) {
    ~chatnpc("<p,neutral>It can. Though I'll admit, I never thought I'd see the day. Silverlight was forged to banish demons, not bind their remains.");
    ~chatplayer("<p,quiz>Will it be enough to face Agrith-Vhann?");
    ~chatnpc("<p,neutral>If anything can be, this will. But understand this, once the ashes are bound, there is no undoing it. The blade will carry a trace of chaos forever.");
    ~chatplayer("<p,neutral>I'm ready.");
    @demon_slayer_2_combine_ashes_with_silverlight;
} else {
    ~chatnpc("<p,angry>Try bringing me all the ashes first and we'll talk.");
}

[label,demon_slayer_2_combine_ashes_with_silverlight]
if (inv_total(inv, silverlight) < 1) {
    ~chatnpc("<p,angry>You're lucky one of my servants found Silverlight and returned it to me. Oh well, let's get on with it.");

}
inv_del(inv, infernal_ashes, 1);
inv_del(inv, ritual_ashes, 1);
inv_del(inv, sovereign_ashes, 1);
inv_del(inv, silverlight, 1);
sound_synth(enchant_diamond_ring, 0, 0);
npc_anim(human_cast_enchantring, 0);
inv_add(inv, ashbound_silverlight, 1);
%demon2start = ^demon2_ashes_combined;
~chatnpc("<p,neutral>It is done.");
~chatplayer("<p,neutral>Thank you, Sir Prysin. I feel more prepared to face Agrith-Vhann now.");
@demon_slayer_2_where_agrith_vhann_is;

[label,demon_slayer_2_where_agrith_vhann_is]
~chatnpc("<p,neutral>The citizens of Varrock have been noticing Zamorakians heading to west towards ice mountain. Perhaps you should investigate the fortress that sits by its' side");
~chatplayer("<p,neutral>Thank you, I will head there now.");

[label,demon_slayer_2_ask_about_demon]
~chatplayer("<p,shock>Sir Prysin, Gypsy Aris says there's trouble, and a demon called Agrith-Vhann?");
~chatnpc("<p,sad>Agrith-Vhann? Saradomin save us... I was hoping she hadn't seen that.");
~chatplayer("<p,angry>So you knew something was happening?");
~chatnpc("<p,neutral>Only rumours. Whispers. Strange movements among Zamorakian fanatics. But if Aris saw Agrith-Vhann's flames... then it's already begun.");
def_int $option = ~p_choice2("Who is Agrith-Vhann?", 1, "A world ending demon is sadly above my paygrade.", 2);
if ($option = 1) {
        ~chatplayer("<p,neutral>Who is Agrith-Vhann?");
        @demon_slayer_2_who_is_agrith_vhann;
    } else if ($option = 2) {
        ~chatplayer("<p,neutral>A world ending demon is sadly above my paygrade.");
        ~chatnpc("<p,neutral>Suit yourself.");
    }

[label,demon_slayer_2_who_is_agrith_vhann]
~chatnpc("<p,neutral>One of Zamoraks' ancient archdemons. Far beyond stronger than Delrith. Far beyond anything Silverlight was ever meant to face. Legends say he commanded Zamoraks demonic forces during the God Wars.");
~chatplayer("<p,neutral>So how is he returning now?");
~chatnpc("<p,neutral>He isn't, yet. He can't enter RuneScape without his essence properly anchored, and that requires infernal flames. Zamorakian cultists have been grouping together, trying to summon him back.");
~chatplayer("<p,quiz>What do you need me to do?");
~chatnpc("<p,neutral>We must strengthen Silverlight. It's original blessing was imbued for lesser demons like Delrith, not archdemons like Agrith-Vhann. To reforge it, we'll need infernal energy, and thankfully, the cultists are providing plenty of that.");
~chatplayer("<p,neutral>Where do I begin?");
~chatnpc("<p,neutral>I've heard some rumours of Zamorakians gathering in the rundown Zamorakian temple in the city. Perhaps you will find some clues there.");
~chatplayer("<p,neutral>I'll head there right away.");
%demon2start = ^demon2_ask_prysin_about_demon;


[label,demon_slayer_sir_prysin_pre_silverlight]
~chatnpc("<p,neutral>Hello, who are you?");
def_int $option = ~demon_slayer_sir_prysin_options(%demonstart);

if ($option = 1) {
    ~chatplayer("<p,neutral>I am a mighty adventurer, who are you?");
    ~chatnpc("<p,neutral>I am Sir Prysin. A bold and famous knight of the realm.");
} else if ($option = 2) {
    ~chatplayer("<p,confused>I was hoping you could tell me.");
    ~chatnpc("<p,neutral>Well I've never met you before.");
} else {
    ~chatplayer("<p,neutral>Gypsy Aris said I should come and talk to you.");
    @demon_slayer_sir_prysin_aris;
}

[label,demon_slayer_sir_prysin_key_search_progress]
~chatnpc("<p,neutral>So how are you doing with getting the keys?");

if (inv_total(inv, silverlight_key_1) > 0 & inv_total(inv, silverlight_key_2) > 0 & inv_total(inv, silverlight_key_3) > 0) {
    @demon_slayer_prysin_got_them;
} else if (inv_total(inv, silverlight_key_1) < 1 & inv_total(inv, silverlight_key_2) < 1 & inv_total(inv, silverlight_key_3) < 1) {
    ~chatplayer("<p,happy>I've not found any of them yet.");
} else {
    ~chatplayer("<p,neutral>I've made a start.");
}
if (inv_total(inv, silverlight_key_1) > 0) {
    ~chatplayer("<p,neutral>I've got the key off Wizard Traiborn.");
}

if (inv_total(inv, silverlight_key_2) > 0) {
    ~chatplayer("<p,neutral>I've got the key off Captain Rovin.");
}

if (inv_total(inv, silverlight_key_3) > 0) {
    ~chatplayer("<p,neutral>I've got the key You dropped down the drain.");
}

def_int $option = ~p_choice2("Can you remind me where all they keys were again?", 1, "I'm still looking.", 2);

if ($option = 1) {
    ~chatplayer("<p,neutral>Can you remind me where all they keys were again?");
    @demon_slayer_sir_prysin_keys;
} else {
    ~chatplayer("<p,neutral>I'm still looking.");
    ~chatnpc("<p,neutral>Ok, tell me when you've got them all.");
}

[label,demon_slayer_prysin_got_them]
~chatplayer("<p,happy>I've got all three keys!");
~chatnpc("<p,happy>Excellent! Now I can give you Silverlight.");
if_close();
mes("You give all three keys to Sir Prysin.");
inv_del(inv, silverlight_key_1, 1);
inv_del(inv, silverlight_key_2, 1);
inv_del(inv, silverlight_key_3, 1);
p_delay(2);
mes("Prysin hands you an impressive looking sword.");
p_delay(2);
inv_add(inv, silverlight, 1);
%demonstart = ^demon_silverlight;

[label,demon_slayer_sir_prysin_aris]
~chatnpc("<p,neutral>Gypsy Aris? Is she still alive? I remember her from when I was pretty young. Well what do you need to talk to me about?");
def_int $option = ~p_choice2("I need to find Silverlight.", 1, "Yes, she is still alive.", 2);

if ($option = 1) {
    @demon_slayer_sir_prysin_silverlight;
} else if ($option = 2) {
    ~chatplayer("<p,happy>Yes she is still alive. She lives right outside the castle!");
    ~chatnpc("<p,neutral>I would have thought she would have died by now. She was pretty old when I was a lad.");
    ~chatnpc("<p,neutral>Anyway, what can I do for you?");
    @demon_slayer_sir_prysin_silverlight;
}

[label,demon_slayer_sir_prysin_silverlight]
~chatplayer("<p,neutral>I need to find Silverlight.");
~chatnpc("<p,neutral>What do you need to find that for?");
~chatplayer("<p,neutral>I need it to fight Delrith.");
~chatnpc("<p,neutral>Delrith? I thought the world was rid of him.");
def_int $option = ~p_choice2("Well, the gypsy's crystal ball seems to think otherwise.", 1, "He's back and unfortunately I've got to deal with him.", 2);

if ($option = 1) {
    ~chatplayer("<p,neutral>Well the gypsy's crystal ball seems to think otherwise.");
    ~chatnpc("<p,neutral>Well if the ball says so, I'd better help you.");
} else if ($option = 2) {
    ~chatplayer("<p,sad>He's back and unfortunately I've got to deal with him.");
    ~chatnpc("<p,neutral>You don't look up to much. I suppose Silverlight may be good enough to carry you through though.");
}
~chatnpc("<p,neutral>The problem is getting Silverlight.");
~chatplayer("<p,sad>You mean you don't have it?");
~chatnpc("<p,neutral>Oh I do have it, but it is so powerful that I have to put it in a special box which needs three different keys to open it. That way it won't fall into the wrong hands.");
def_int $option_keys = ~p_choice2("So give me the keys!", 1, "And why is this a problem?", 2);

if ($option_keys = 1) {
    ~chatplayer("<p,quiz>So give me the keys!");
    ~chatnpc("<p,neutral>Um, well it's not so easy.");
    @demon_slayer_sir_prysin_keys;
} else if ($option_keys = 2) {
    ~chatplayer("<p,neutral>And why is this a problem?");
    @demon_slayer_sir_prysin_keys;
}

[label,demon_slayer_sir_prysin_keys]
~chatnpc("<p,neutral>I kept one of the keys. I gave the other two to other people for safe keeping.");
~chatnpc("<p,neutral>One I gave to Rovin, the captain of the palace guard.");
~chatnpc("<p,neutral>I gave the other to the wizard Traiborn.");
%demonstart = ^demon_key_hunt;
def_int $option_keys = ~p_choice3("Can you give me your key?", 1, "Where can I find Captain Rovin?", 2, "Where does the wizard live?", 3);

if ($option_keys = 1) {
    @demon_slayer_sir_prysin_key_inquiry;
} else if ($option_keys = 2) {
    @demon_slayer_sir_captain_rovin_key_inquiry;
} else if ($option_keys = 3) {
    @demon_slayer_sir_wizard_traiborn_inquiry;
}

[label,demon_slayer_sir_prysin_key_inquiry]
~chatplayer("<p,neutral>Can you give me your key?");
~chatnpc("<p,sad>Um.... ah....");
~chatnpc("<p,sad>Well there's a problem there as well.");
~chatnpc("<p,sad>I managed to drop the key in the drain just outside the palace kitchen. It is just inside and I can't reach it.");
def_int $option = ~p_choice3("So what does the drain lead to?", 1, "Where can I find Captain Rovin?", 2, "Where does the wizard live?", 3);

if ($option = 1) {
    @demon_slayer_sir_prysin_drain;
} else if ($option = 2) {
    @demon_slayer_sir_captain_rovin_key_inquiry;
} else if ($option = 3) {
    @demon_slayer_sir_wizard_traiborn_inquiry;
}

[label,demon_slayer_sir_captain_rovin_key_inquiry]
~chatplayer("<p,neutral>Where can I find Captain Rovin?");
~chatnpc("<p,neutral>Captain Rovin lives at the top of the guards' quarters in the north-west wing of this palace.");
def_int $option = ~p_choice3("Can you give me your key?", 1, "Where does the wizard live?", 2, "Well I'd better go key hunting.", 3);

if ($option = 1) {
    @demon_slayer_sir_prysin_key_inquiry;
} else if ($option = 2) {
    @demon_slayer_sir_wizard_traiborn_inquiry;
} else {
    @demon_slayer_better_go_key_hunting;
}

[label,demon_slayer_sir_wizard_traiborn_inquiry]
~chatplayer("<p,neutral>Where does the wizard live?");
~chatnpc("<p,neutral>Wizard Traiborn?");
~chatnpc("<p,neutral>He is one of the wizards who lives in the tower on the little island just off the south coast. I believe his quarters are on the first floor of the tower.");
def_int $option = ~p_choice3("Can you give me your key?", 1, "Where can I find Captain Rovin?", 2, "Well I'd better go key hunting.", 3);

if ($option = 1) {
    @demon_slayer_sir_prysin_key_inquiry;
} else if ($option = 2) {
    @demon_slayer_sir_captain_rovin_key_inquiry;
} else {
    @demon_slayer_better_go_key_hunting;
}

[label,demon_slayer_sir_prysin_drain]
~chatplayer("<p,neutral>So what does the drain connect to?");
~chatnpc("<p,neutral>It is the drain for the drainpipe running from the sink in the kitchen down to the palace sewers.");
def_int $option = ~p_choice3("Where can I find Captain Rovin?", 1, "Where does the wizard live?", 2, "Well I'd better go key hunting.", 3);

if ($option = 1) {
    @demon_slayer_sir_captain_rovin_key_inquiry;
} else if ($option = 2) {
    @demon_slayer_sir_wizard_traiborn_inquiry;
} else {
    @demon_slayer_better_go_key_hunting;
}

[label,demon_slayer_better_go_key_hunting]
~chatplayer("<p,happy>Well I'd better go key hunting.");
~chatnpc("<p,happy>Ok, goodbye.");

[label,demon_slayer_need_to_kill]
~chatnpc("<p,neutral>You sorted out that demon yet?");
if (~demon_slayer_has_silverlight = true) {
    @demon_slayer_not_sorted_yet;
}
else{
    def_int $option = ~p_choice2("No, not yet.", 1, "No, I'm afraid I've lost Silverlight.", 2);
    if ($option = 1){
        @demon_slayer_not_sorted_yet;
    }
    else if ($option = 2){
        ~chatplayer("<p,sad>No, I'm afraid I've lost Silverlight.");
        // osrs dialogue
        ~chatnpc("<p,neutral>Yes, I know, someone returned it to me. |Take better care of it this time.");
        inv_add(inv, silverlight, 1);
    }
}

[label,demon_slayer_not_sorted_yet]
    ~chatplayer("<p,happy>No, not yet.");
    ~chatnpc("<p,neutral>Well get on with it. He'll be pretty powerful when he gets to full strength.");

[label,demon_slayer_2_completed]
~chatnpc("<p,happy>You've done well to stop Agrith-Vhann. The realm is in your debt once again.");
~chatplayer("<p,happy>Thank you, Sir Prysin. It was a tough battle.");
if (~demon_slayer_2_has_ashbound_silverlight = false) {
    ~chatnpc("<p,neutral>By the way, I noticed you no longer have Silverlight. Did you lose it?");
    ~chatplayer("<p,neutral>Yes, I'm afraid I did.");
    inv_add(inv, ashbound_silverlight, 1);
    ~chatnpc("<p,neutral>Hmm. Well fortunately one of my servants found Silverlight and returned it to me. |Take better care of it this time.");
}


[label,demon_slayer_completed]
if (%demon2start > 0) {
    ~chatnpc("<p,happy>Hello again, mighty adventurer!");
    ~chatnpc("<p,happy>Please keep me updated on any news about Agrith-Vhann. The city depends on it.");
} else {
    ~chatnpc("<p,happy>Hello. I've heard you stopped the demon, well done!");
}
if (~demon_slayer_has_silverlight = true){
    @demon_slayer_boast;
}

else{
    def_int $option = ~p_choice2("Yes, that's right.", 1, "Yes, although I'm afraid I've lost Silverlight.", 2);
    if ($option = 1){
        @demon_slayer_boast;
    }
    else{
        ~chatplayer("<p,neutral>Yes, although I'm afraid I've lost Silverlight.");
        ~chatnpc("<p,angry>Yes, news of your carelessness is as widespread as |news of your victory. Fortunately for you, Silverlight| has come back into my possession.");
        $option = ~p_choice2("Phew, that's a relief!", 1, "Is there any chance of me borrowing it again?", 2);
        if ($option = 1){
            ~chatplayer("<p, neutral> Phew, that's a relief!");
        }
        else if ($option = 2){
            ~chatplayer("<p,neutral>Is there any change of me borrowing it again?");
            ~chatnpc("<p,neutral>I won't give it away that easily again. It's far too|important to be treated so disrespectfully.");
            ~chatnpc("<p,neutral>If you with to make use of Silverlight again, it will cost|you 500 gold pieces. Maybe that will encourage you to|look after it.");
            $option = ~p_choice2("No way, it's not worth that much.", 1, "Okay, I'll pay.", 2);
            if ($option = 1){
                // Unknown what else should be here
                ~chatplayer("<p,neutral>No way, it's not worth that much.");
            }
            if ($option = 2){
                @demon_slayer_buy_silverlight;
            }
        }
    }
}


[label,demon_slayer_boast]
~chatplayer("<p,neutral>Yes, that's right.");
~chatnpc("<p,happy>A good job well done.");
~chatplayer("<p,happy>Thank you.");

[label,demon_slayer_buy_silverlight]
~chatplayer("<p,neutral>Okay, I'll pay.");
if (inv_total(inv, coins) >= 500){
    inv_del(inv, coins, 500);
    inv_add(inv, silverlight, 1);
    ~chatnpc("May you make good use of it.");
}
else{
    // Inauthentic what should happen here
    ~chatnpc("<p,angry>You don't even have enough money.");
}

[proc,demon_slayer_sir_prysin_options](int $demon_progress)(int)
if ($demon_progress = ^demon_not_started) {
    return(~p_choice2("I am a mighty adventurer. Who are you?", 1, "I'm not sure, I was hoping you could tell me.", 2));
} else if ($demon_progress = ^demon_talked_aris) {
    return(~p_choice3("I am a mighty adventurer. Who are you?", 1, "I'm not sure, I was hoping you could tell me.", 2, "Gypsy Aris said I should come and talk to you.", 3));
}


[proc,demon_slayer_has_silverlight]()(boolean)
// Turns out you can't put all of this in a return statement
if (inv_total(inv, silverlight) > 0 | (inv_getobj(worn, 3) = silverlight) | inv_total(bank, silverlight) > 0 ) {
    return(true);
} else {
    return(false);
}

[proc,demon_slayer_2_has_ashbound_silverlight]()(boolean)
// Turns out you can't put all of this in a return statement
if (inv_total(inv, ashbound_silverlight) > 0 | (inv_getobj(worn, 3) = ashbound_silverlight) | inv_total(bank, ashbound_silverlight) > 0 ) {
    return(true);
} else {
    return(false);
}

[proc,demon_slayer_2_check_if_has_ashes]()(boolean)
// Turns out you can't put all of this in a return statement
if (inv_total(inv, infernal_ashes) > 0 & inv_total(inv, ritual_ashes) > 0 & inv_total(inv, sovereign_ashes) > 0 ) {
    return(true);
} else {
    return(false);
}
