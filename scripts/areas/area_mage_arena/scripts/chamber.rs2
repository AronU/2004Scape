[oploc1,magearena_statue_saradomin] @pray_at_statue("Saradomin", saradomin_cape);
[oploc1,magearena_statue_zamorak] @pray_at_statue("Zamorak", zamorak_cape);
[oploc1,magearena_statue_guthix] @pray_at_statue("Guthix", guthix_cape);

[label,pray_at_statue](string $name, namedobj $cape)
if (inzone(0_40_79_30_33, 0_40_79_33_35, coord) = true) {
    anim(human_pray, 0);
    sound_synth(prayer_boost, 0, 0);
    if (%demon2start ! ^demon2_read_scroll_2) {
        return;
    }
    npc_huntall(coord, 20, 0);
    while (npc_findnext = true) {
        if (npc_type = firewall) {
            %respawn_timer_quest = 5;
        }
        if (npc_type = demon2_monk_3) {
            return;
        }
    }
    mes("The walls of fire around you dissipate as you pray to the statue.");
    npc_add(0_40_79_31_33, demon2_monk_3, 500);
    spotanim_map(smokepuff, 0_40_79_31_33, 0, 0);
    return;
}
~forcewalk2(movecoord(loc_coord, 0, 0, -2));
facesquare(loc_coord);
if (~obj_gettotalcat(armour_godcape) > 0 | ~check_ground_godcapes = true) { // already have a cape
    anim(human_pray, 10);
    p_delay(2);
    ~mesbox("You kneel and chant to <$name>...|... but there is no response.");
    return;
}
~mesbox("You kneel and begin to chant to <$name>...");
if (%magearena = ^mage_arena_complete) %magearena = ^mage_arena_prayed_at_statue;
anim(human_pray, 10);
p_delay(2);
// always obj_add's https://youtu.be/fJVqiCdunZw?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o
obj_add(movecoord(loc_coord, 0, 0, -1), $cape, 1, 200); // 50 ticks in osrs but i think its a late 2006 patch to prevent multiple capes?
spotanim_map(smokepuff_large, movecoord(loc_coord, 0, 0, -1), 60, 0);
sound_synth(smokepuff, 0, 0);
~mesbox("You feel a rush of energy charge through your veins. Suddenly a|cape appears before you.");

[proc,check_ground_godcapes]()(boolean)
if (obj_find(0_39_73_4_48, saradomin_cape) = true) {
    return (true);
}
if (obj_find(0_39_73_11_51, guthix_cape) = true) {
    return (true);
}
if (obj_find(0_39_73_20_48, zamorak_cape) = true) {
    return (true);
}
