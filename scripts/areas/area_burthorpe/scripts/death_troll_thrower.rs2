// wiki says these melee but I can't get them to do so?
[ai_queue1,_troll_thrower] ~npc_default_retaliate_ap;
[ai_opplayer2,_troll_thrower] npc_setmode(applayer2);

// these are a bit odd, because they never miss with ranged
[ai_applayer2,_troll_thrower]
if(random(35) = 0) npc_say("Urg!"); // 7 times in 244 ticks (maybe get more data), its not (always) on attack cycle
if (%npc_action_delay > map_clock) {
    npc_setmode(applayer2);
    return;
}
if (~npc_check_notcombat = false) {
    npc_setmode(null);
    return;
}
if (~npc_check_notcombat_self = false) {
    return;
}

def_int $maxhit = ~npc_ranged_maxhit;

npc_anim(npc_param(attack_anim), 0);
sound_synth(npc_param(rangeattack_sound), 1, 0); // area sound?
spotanim_npc(npc_param(proj_launch), npc_param(proj_height), 0);

%npc_action_delay = add(map_clock, npc_param(attackrate));
def_int $duration = 64;
// map_projanim_v2	id=troll_rock_travel (276), starttime=31, endtime=50, angle=8, progress=128, startheight=146, endheight=146
$duration = ~player_projectile(npc_coord, coord, uid, npc_param(proj_travel), 36, 36, 31, 8, 15, 128, 1);

if (~check_protect_prayer(^ranged_style) = false) { // bypasses defence, but checks prayer
    ~playerhit_n_ranged(randominc($maxhit), $duration, npc_param(attackrate));
    return;
}
~playerhit_n_ranged(0, $duration, npc_param(attackrate));

[ai_queue3,_troll_thrower] @troll_drop_table;