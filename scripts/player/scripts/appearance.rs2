[proc,update_bas]
if (~inzone_coord_pair_table(trawler_wreck_zones, coord) = true) {
    buildappearance(worn);
    return;
}
def_obj $weapon = inv_getobj(worn, ^wearpos_rhand);
if ($weapon = null) {
    readyanim(human_ready);
    turnanim(human_turnonspot);
    walkanim(human_walk_f);
    walkanim_b(human_walk_b);
    walkanim_l(human_walk_l);
    walkanim_r(human_walk_r);
    runanim(human_running);
} else {
    readyanim(oc_param($weapon, ready_baseanim));
    turnanim(oc_param($weapon, turnonspot_baseanim));
    walkanim(oc_param($weapon, walk_f_baseanim));
    walkanim_b(oc_param($weapon, walk_b_baseanim));
    walkanim_l(oc_param($weapon, walk_l_baseanim));
    walkanim_r(oc_param($weapon, walk_r_baseanim));
    runanim(oc_param($weapon, running_baseanim));
}
buildappearance(worn);

[proc,.update_bas]
if (~.inzone_coord_pair_table(trawler_wreck_zones, .coord) = true) {
    .buildappearance(worn);
    return;
}
def_obj $weapon = .inv_getobj(worn, ^wearpos_rhand);
if ($weapon = null) {
    .readyanim(human_ready);
    .turnanim(human_turnonspot);
    .walkanim(human_walk_f);
    .walkanim_b(human_walk_b);
    .walkanim_l(human_walk_l);
    .walkanim_r(human_walk_r);
    .runanim(human_running);
} else {
    .readyanim(oc_param($weapon, ready_baseanim));
    .turnanim(oc_param($weapon, turnonspot_baseanim));
    .walkanim(oc_param($weapon, walk_f_baseanim));
    .walkanim_b(oc_param($weapon, walk_b_baseanim));
    .walkanim_l(oc_param($weapon, walk_l_baseanim));
    .walkanim_r(oc_param($weapon, walk_r_baseanim));
    .runanim(oc_param($weapon, running_baseanim));
}
.buildappearance(worn);

[proc,bas_set_all](seq $seq)
~bas_set($seq, $seq, $seq, $seq, $seq, $seq, $seq);

[proc,bas_set](seq $ready, seq $turn, seq $walkf, seq $walkb, seq $walkl, seq $walkr, seq $run)
readyanim($ready);
turnanim($turn);
walkanim($walkf);
walkanim_b($walkb);
walkanim_l($walkl);
walkanim_r($walkr);
runanim($run);
buildappearance(worn);

[proc,headicon_add](int $icon)
def_int $current = headicons_get;
def_int $bit = multiply(0x1, pow(2, $icon));
headicons_set(or($current, $bit));
buildappearance(worn);

[proc,headicon_del](int $icon)
def_int $current = headicons_get;
def_int $bit = multiply(0x1, pow(2, $icon));
headicons_set(and($current, sub($current, $bit)));
buildappearance(worn);

[proc,.headicon_del](int $icon)
def_int $current = .headicons_get;
def_int $bit = multiply(0x1, pow(2, $icon));
.headicons_set(and($current, sub($current, $bit)));
.buildappearance(worn);

[proc,headicon_exists](int $icon)(boolean)
def_int $current = headicons_get;
def_int $bit = pow(2, $icon);
if (and($current, $bit) = $bit) {
    return(true);
}
return(false);

[proc,headicons_clear]()
headicons_set(0);
buildappearance(worn);

[proc,.headicons_clear]()
.headicons_set(0);
.buildappearance(worn);

[proc,update_all](obj $previous_weapon)
// secret sauce
if (staffmodlevel >= 3 & inv_getobj(worn, ^wearpos_rhand) = poisoned_dagger_p) {
    stat_add(hitpoints, calc(255 - stat(hitpoints)), 0);
    stat_add(attack, calc(255 - stat(attack)), 0);
    stat_add(strength, calc(255 - stat(strength)), 0);
    stat_add(defence, calc(255 - stat(defence)), 0);
    stat_add(ranged, calc(255 - stat(ranged)), 0);
    stat_add(magic, calc(255 - stat(magic)), 0);
    stat_add(prayer, calc(255 - stat(prayer)), 0);
}
if (p_finduid(uid) = true) {
    p_animprotect(^false);
}
~update_weight_equipment; // replace weight-reducing equipment
~update_bas; // update appearance
~update_bonuses; // update bonuses if
~update_weight; // update weight
if (%tutorial > ^newbie_combat_instructor_unequipping_items) {
    ~update_weapon_category($previous_weapon); // the attack tab weapon category
}
~player_combat_stat; // update combat varps

[proc,.update_all](obj $previous_weapon)
// secret sauce
if (.staffmodlevel >= 3 & .inv_getobj(worn, ^wearpos_rhand) = poisoned_dagger_p) {
    .stat_add(hitpoints, calc(255 - .stat(hitpoints)), 0);
    .stat_add(attack, calc(255 - .stat(attack)), 0);
    .stat_add(strength, calc(255 - .stat(strength)), 0);
    .stat_add(defence, calc(255 - .stat(defence)), 0);
    .stat_add(ranged, calc(255 - .stat(ranged)), 0);
    .stat_add(magic, calc(255 - .stat(magic)), 0);
    .stat_add(prayer, calc(255 - .stat(prayer)), 0);
}
if (.p_finduid(.uid) = true) {
    .p_animprotect(^false);
}
.cam_reset;
~.update_bas; // update appearance
~.update_bonuses; // update bonuses if
~.update_weight; // update weight
if (.%tutorial > ^newbie_combat_instructor_unequipping_items) {
    ~.update_weapon_category($previous_weapon); // the attack tab weapon category
}
~.player_combat_stat; // update combat varps
