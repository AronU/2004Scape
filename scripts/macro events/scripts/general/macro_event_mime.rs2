// MIME RANDOM EVENT
// Entry points:
// macro_mysterious_old_man - the man who will abduct you
// mapzone - when you enter the mime area, or log into the mime area
// ai_timer - the mime npc has a state timer that globally shows the emote to follow
// if_button - the buttons when you click on the interface with all the emotes

// Mysterious Old Man:
[proc,mime_entry_point]
%macro_event_target_coord = coord;
p_teleport(0_31_74_24_28);
~macro_events_set_tabs; //Was not originally in mapzone trigger I think. https://www.youtube.com/watch?v=ROQJPpb7Qrc
~chatnpc("<p,neutral>You need to copy the mime's performance, then you'll be returned to where you were.");

// Enter map trigger: 
[mapzone,0_31_74]
%macro_event = ^macro_mime;
midi_song("artistry"); // Todo: In cache but was it played here without being unlockable?
if_settext(music:com_177, "Artistry");
def_int $count = divide(modulo(map_clock, multiply(16,2)), 2);
if ($count < 4) {
    if (loc_find(0_31_74_23_25, loc_3644) = true) {
        loc_anim(seq_1135);
    }
    if (loc_find(0_31_74_26_25, loc_3644) = true) {
        loc_anim(seq_1136);
    }
} else {
    if (loc_find(0_31_74_23_25, loc_3644) = true) {
        loc_anim(seq_1136);
    }
    if (loc_find(0_31_74_26_25, loc_3644) = true) {
        loc_anim(seq_1135);
    }
}
mes("You need to copy the mime's performance, then you'll return to");
mes("where you were.");

// Exit map trigger: restore tabs
[mapzoneexit,0_31_74]
~initalltabs;
~macro_events_varps_reset;

[ai_timer,npc_1056]
def_int $count = divide(modulo(map_clock, multiply(16,2)), 2);
if($count = 0) {
    npc_facesquare(movecoord(npc_coord, 0, 0, -1));
    if (loc_find(0_31_74_23_25, loc_3644) = true) {
        loc_anim(seq_1135);
    }
    if (loc_find(0_31_74_26_25, loc_3644) = true) {
        loc_anim(seq_1136);
    }
    huntall(0_31_74_24_28, 1, 0);
    while(huntnext = true) {
        if_close();
        queue(mime_step_right_up, 0, 0);
    }
    huntall(0_31_74_24_26, 1, 0);
    while (huntnext = true) {
        if_close();
    }
}
if($count = 1) {
    %npc_int = random(8);
    switch_int(%npc_int) {
        case 0 : npc_anim(emote_cry, 0);
        case 1 : npc_anim(emote_think, 0);
        case 2 : npc_anim(emote_laugh, 0);
        case 3 : npc_anim(emote_dance, 0);
        case 4 : npc_anim(emote_climbing_rope, 0);
        case 5 : npc_anim(emote_mime_lean, 0);
        case 6 : npc_anim(emote_glass_wall, 0);
        case 7 : npc_anim(emote_glass_box, 0);
    }
}
if($count = 4) {
    npc_anim(emote_bow, 0);
    npc_facesquare(movecoord(npc_coord, -1, 0, 0));
    if (loc_find(0_31_74_23_25, loc_3644) = true) {
        loc_anim(seq_1136);
    }
    if (loc_find(0_31_74_26_25, loc_3644) = true) {
        loc_anim(seq_1135);
    }
    huntall(0_31_74_24_26, 1, 0);
    while (huntnext = true) {
        if_openchat(macro_mime_emotes);
    }

}
npc_settimer(2); // Called every 2 ticks


[queue,mime_step_right_up]
p_teleport(0_31_74_24_26);


// Trigger: All the button clicks
[if_button,macro_mime_emotes:com_2] @mime_random_emote(emote_cry,0);
[if_button,macro_mime_emotes:com_3] @mime_random_emote(emote_think,1);
[if_button,macro_mime_emotes:com_4] @mime_random_emote(emote_laugh,2);
[if_button,macro_mime_emotes:com_5] @mime_random_emote(emote_dance,3);
[if_button,macro_mime_emotes:com_6] @mime_random_emote(emote_climbing_rope,4);
[if_button,macro_mime_emotes:com_7] @mime_random_emote(emote_mime_lean,5);
[if_button,macro_mime_emotes:com_8] @mime_random_emote(emote_glass_wall,6);
[if_button,macro_mime_emotes:com_9] @mime_random_emote(emote_glass_box,7);

// Label to check if the right emote is selected
[label,mime_random_emote](seq $anim, int $num)
if_close;
anim($anim, 0);

def_int $curr_emote = random(8);
if (npc_find(0_31_74_27_26, npc_1056, 1, 0) = true) {
    $curr_emote = %npc_int;
}

if ($num = $curr_emote) {
    p_delay(4);
    anim(emote_cheer, 0);
    // https://www.youtube.com/watch?v=Y0AsFEr6sxY 4 correct in a row to leave
    // https://www.youtube.com/watch?v=yWcG5T3KEUQ 4 correct in a row to leave
    switch_int(%macro_event) {
        case ^macro_mime : %macro_event = ^macro_mime_solve_1;
        case ^macro_mime_solve_1 : %macro_event = ^macro_mime_solve_2;
        case ^macro_mime_solve_2 : %macro_event = ^macro_mime_solve_3;
        case ^macro_mime_solve_3 :
        // Return where you are from
        p_delay(4);
        p_teleport(%macro_event_target_coord);
        def_int $rand = random(2);
        if($rand = 0 & ~has_all_mime_emotes = false) {
            ~unlock_mime_emote;
        } else if($rand = 1 & ~has_full_mime_outfit = true & ~has_all_mime_emotes = false) {
            ~unlock_mime_emote;
        } else {
            inv_add(inv, ~macro_mime_reward);
        }
    }
} else {
    p_delay(4);
    anim(emote_cry, 0);
    %macro_event = ^macro_mime;
}

[proc,macro_mime_reward]()(namedobj, int)
if (~has_full_mime_outfit = false) {
    return (~unlock_mime_outfit_piece);
} else {
    return(coins, 500);
}

[proc,has_full_mime_outfit]()(boolean)
if(inv_total(inv, macro_mime_mask) = 0 & inv_total(worn, macro_mime_mask) = 0 & inv_total(bank, macro_mime_mask) = 0) {
    return (false);
}
if(inv_total(inv, macro_mime_top) = 0 & inv_total(worn, macro_mime_top) = 0 & inv_total(bank, macro_mime_top) = 0) {
    return (false);
}
if (inv_total(inv, macro_mime_legs) = 0 & inv_total(worn, macro_mime_legs) = 0 & inv_total(bank, macro_mime_legs) = 0) {
    return (false);
}
if (inv_total(inv, macro_mime_gloves) = 0 & inv_total(worn, macro_mime_gloves) = 0 & inv_total(bank, macro_mime_gloves) = 0) {
    return (false);
}
if (inv_total(inv, macro_mime_gloves) = 0 & inv_total(worn, macro_mime_gloves) = 0 & inv_total(bank, macro_mime_gloves) = 0) {
    return (false);
}
return (true);

[proc,has_all_mime_emotes]()(boolean)
if(testbit(%emote_access, ^macro_mime_climb_rope) = ^false) {
    return (false);
}
if(testbit(%emote_access, ^macro_mime_lean) = ^false) {
    return (false);
}
if(testbit(%emote_access, ^macro_mime_glass_wall) = ^false) {
    return (false);
}
if(testbit(%emote_access, ^macro_mime_glass_box) = ^false) {
    return (false);
}
return (true);

[proc,unlock_mime_emote]
def_int $missing_count = 0;

if (testbit(%emote_access, ^macro_mime_climb_rope) = ^false) {
    $missing_count = add($missing_count, 1);
}
if (testbit(%emote_access, ^macro_mime_lean) = ^false) {
    $missing_count = add($missing_count, 1);
}
if (testbit(%emote_access, ^macro_mime_glass_wall) = ^false) {
    $missing_count =add($missing_count, 1);
}
if (testbit(%emote_access, ^macro_mime_glass_box) = ^false) {
    $missing_count = add($missing_count, 1);
}

def_int $pick = random($missing_count);
def_int $current = 0;

if (testbit(%emote_access, ^macro_mime_climb_rope) = ^false) {
    if ($current = $pick) {
        %emote_access = setbit(%emote_access, ^macro_mime_climb_rope);
        ~mesbox("You can now use the Climb Rope emote!");
        return;
    }
    $current = add($current, 1);
}
if (testbit(%emote_access, ^macro_mime_lean) = ^false) {
    if ($current = $pick) {
        %emote_access = setbit(%emote_access, ^macro_mime_lean);
        ~mesbox("You can now use the Lean on air emote!");
        return;
    }
    $current = add($current, 1);
}
if (testbit(%emote_access, ^macro_mime_glass_wall) = ^false) {
    if ($current = $pick) {
        %emote_access = setbit(%emote_access, ^macro_mime_glass_wall);
        ~mesbox("You can now use the Glass Wall emote!");
        return;
    }
    $current = add($current, 1);
}
if (testbit(%emote_access, ^macro_mime_glass_box) = ^false) {
    if ($current = $pick) {
        %emote_access = setbit(%emote_access, ^macro_mime_glass_box);
        ~mesbox("You can now use the Glass Box emote!");
        return;
    }
}

[proc,unlock_mime_outfit_piece]()(namedobj, int)
def_int $missing_count = 0;

if (inv_total(inv, macro_mime_mask) = 0 & inv_total(worn, macro_mime_mask) = 0 & inv_total(bank, macro_mime_mask) = 0) {
    $missing_count = calc($missing_count + 1);
}
if (inv_total(inv, macro_mime_top) = 0 & inv_total(worn, macro_mime_top) = 0 & inv_total(bank, macro_mime_top) = 0) {
    $missing_count = calc($missing_count + 1);
}
if (inv_total(inv, macro_mime_legs) = 0 & inv_total(worn, macro_mime_legs) = 0 & inv_total(bank, macro_mime_legs) = 0) {
    $missing_count = calc($missing_count + 1);
}
if (inv_total(inv, macro_mime_gloves) = 0 & inv_total(worn, macro_mime_gloves) = 0 & inv_total(bank, macro_mime_gloves) = 0) {
    $missing_count = calc($missing_count + 1);
}
if (inv_total(inv, macro_mime_boots) = 0 & inv_total(worn, macro_mime_boots) = 0 & inv_total(bank, macro_mime_boots) = 0) {
    $missing_count = calc($missing_count + 1);
}

def_int $pick = random($missing_count);
def_int $current = 0;

if (inv_total(inv, macro_mime_mask) = 0 & inv_total(worn, macro_mime_mask) = 0 & inv_total(bank, macro_mime_mask) = 0) {
    if ($current = $pick) {
        return(macro_mime_mask, 1);
    }
    $current = add($current, 1);
}
if (inv_total(inv, macro_mime_top) = 0 & inv_total(worn, macro_mime_top) = 0 & inv_total(bank, macro_mime_top) = 0) {
    if ($current = $pick) {
        return(macro_mime_top, 1);
    }
    $current = add($current, 1);
}
if (inv_total(inv, macro_mime_legs) = 0 & inv_total(worn, macro_mime_legs) = 0 & inv_total(bank, macro_mime_legs) = 0) {
    if ($current = $pick) {
        return(macro_mime_legs, 1);
    }
    $current = add($current, 1);
}
if (inv_total(inv, macro_mime_gloves) = 0 & inv_total(worn, macro_mime_gloves) = 0 & inv_total(bank, macro_mime_gloves) = 0) {
    if ($current = $pick) {
        return(macro_mime_gloves, 1);
    }
    $current = add($current, 1);
}
if (inv_total(inv, macro_mime_boots) = 0 & inv_total(worn, macro_mime_boots) = 0 & inv_total(bank, macro_mime_boots) = 0) {
    if ($current = $pick) {
        return(macro_mime_boots, 1);
    }
}
